//===== rAthena Script =======================================
//= OGH Abyss Script
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

gl_cas01_,205,36,4	script	Mage Nillem#Abyssglast2	4_M_JOB_WIZARD,{
	mes .NPCname$;
	mes "เจ้าต้องการที่จะออกจาก";
	mes "Abyss Glastheim Castle หรือไม่?";
	next;
	switch(select(
		""+.sb$+" ไม่ออก",
		""+.sb$+" ออกไปข้างนอก"
	)) {
		case 1: end;
		case 2:
			warp "glast_01",189,268;
			end;
	}
	end;
	
OnInit:
	.NPCname$  = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";
	end;
}

//==================================================
// Quest
//==================================================
glast_01,189,272,4	script	Mage Nillem#glast	4_M_JOB_WIZARD,{
	disable_items;
	mes .NPCname$;
	if(BaseLevel < 175) {
		mes "เจ้ามาทำอะไรที่นี่?";
		mes "ข้ากำลังมองหานักผจญภัยที่แข็งแกร่งเพื่อเติมเต็มความอยากรู้อยากเห็นของข้า!";
		next;
		mes .NPCname$;
		mes "กลับมาเมื่อเจ้ามีเลเวลไม่ต่ำกว่า 175.";
		close;
	}
	mes "ข้าคิดว่าข้าเพิ่งค้นพบสถานที่แห่งหนึ่ง! เจ้าอยากจะรู้เกี่ยวกับมันไหม?";
	next;
	switch(select(
		""+.sb$+" ข้าจะไปที่นั่น!",
		""+.sb$+" ข้าต้องการคำอธิบาย",
		""+.sb$+" แบ่งปันการค้นพบ",
		""+.sb$+" ไม่มีอะไร."
	)) {
		case 1:
			mes .NPCname$;
			mes "นั่นคือทางไป!";
			mes "ข้าดีใจที่ได้เห็นความกระตือรือร้นของเจ้า.";
			close2;
			warp "gl_cas01_",200,26;
			end;
	
		case 2:
			mes .NPCname$;
			mes "มีรายงานเกี่ยวกับพลังเวทย์มนตร์แปลก ๆ ใกล้กับปราสาท Glast Heim.";
			mes "ข้ามาเพื่อสืบสวน และข้าค้นพบบางอย่าง?";
			next;
			mes .NPCname$;
			mes "ช่องว่างในมิติ!";
			mes "พ่อมดอัจฉริยะคนนี้ยังสร้างช่องว่างมิติได้ดีอีกด้วย.";
			next;
			mes .NPCname$;
			mes "ข้าก็เลยบังคับมัน! ข้าเข้าไปข้างใน...";
			mes "กาลเวลาหยุดไหล...";
			mes "อืม. ข้าไม่มีอะไรจะพูดเกี่ยวกับมันอีกแล้ว.";
			next;
			mes .NPCname$;
			mes "อย่างไรก็ตาม, มอนสเตอร์ที่ชั่วร้าย ที่ติดอยู่ข้างในนั้นมากับคลื่นกาลเวลา!";
			next;
			mes .NPCname$;
			mes "ข้าเกือบตาย แต่ก็ออกมาทันเวลา.";
			mes "ข้ากำลังหานักผจญภัยมาร่วมสำรวจด้วยกัน ~ <3";
			close;
	
		case 3:
			mes .NPCname$;
			mes "ข้าไม่แจกภารกิจ ข้าแค่ต้องการค้นพบพื้นที่ใหม่!";
			mes "มีบางอย่างที่ข้าอยากจะทำกับเจ้า โปรดบอกข้าก่อนที่เราจะไปกัน ~ <3";
			next;
			.@q = select("ความว่างเปล่า.:การแช่แข็ง.:เหมือนกระจก:ข้าเสร็จแล้ว.") - 1;
			mes .NPCname$;
			if(.@q == 3) {
				mes "โอ้, ไม่เป็นไร.";
				mes "เหนื่อยนักก็ควรพัก.";
				mes "เจอกันพรุ่งนี้.";
				mes "อย่าหักโหมจนเกินไป ~ <3";
				close;
			}
			switch(checkquest(.abyss_delay[.@q],PLAYTIME)) {
				case 0:
				case 1:
				mes "เจ้ายังคงเหน็ดเหนื่อยจากการค้นพบในอดีต...ควรจะพักผ่อนสักหน่อย.";
				close;
			
				case 2:
				completequest .abyss_delay[.@q];
				erasequest .abyss_delay[.@q];
				mes "เจ้ามีอิสระที่จะแบ่งปันการค้นพบของเจ้ากับข้าอีกครั้ง ~ <3.";
				close;
			}
			switch(checkquest(.abyss_quest[.@q],HUNTING)) {
				case -1:
				mes "นอกเหนือจากช่องว่าง ยังมีสิ่งที่เหมือนว่างเปล่า.";
				switch(.@q) {

					case 0: // Quest: Nothing to see
					mes "^0000FFCorrupted Raydrics^000000 และ ^0000FFCorrupted Raydric Archers^000000...";
					mes "มันเกี่ยวกับการปนเปื้อนของพวกนั้น...";
					mes "มันส่งผลต่อเจ้าอย่างไร?";
					next;
					mes .NPCname$;
					mes "^0000FF20 Corrupted Raydrics,";
					mes "Corrupted Raydric Archers.^000000";
					break;
						
					case 1: // Quest: High Freezing Point
					mes "^0000FFFrozen Gargyoles^000000 และ ^0000FFPrison Breakers^000000...";
					next;
					mes .NPCname$;
					mes "^0000FF5 Frozen Gargyoles,";
					mes "5 Prison Breakers.^000000";
					break;
							
					case 2: // Quest: A Mirror
					mes "^0000FFIce Ghosts^000000 และ ^0000FFFlame Ghosts^000000...";
					mes "ดูแตกต่างอย่างสิ้นเชิงในมิตินี้.";
					next;
					mes .NPCname$;
					mes "^0000FF10 Ice Ghosts,";
					mes "10 Flame Ghosts.^000000";
					break;
				}
				mes "เมื่อไหร่ที่เจ้ากลับมา, ข้าจะตรวจสอบผลลัพธ์ของเจ้า.";
				mes "เป็นยังไงบ้าง?";
				next;
				if(select("ไม่ยากเกินไป:ข้าปฏิเสธ") == 2) {
					mes .NPCname$;
					mes "ไม่ว่างก็ช่วยไม่ได้.";
					mes "ช่วยกันเมื่อมีเวลาว่าง ~ <3";
					close;
				}
				mes .NPCname$;
				mes "มาทำกัน.";
				mes "เกินจุดนี้ รู้ผลยาก.";
				mes "ถ้าต้องการหนีออกจากช่องว่างมิติ มาคุยกับข้าที่ทางเข้า ~ <3";
				setquest .abyss_quest[.@q];
				break;
				
				case 0:
				case 1:
				switch(.@q) {
					case 0:
					mes "เจ้ายังไม่เสร็จสิ้นในการค้นพบ.";
					mes "เจ้าคิดว่าพวกมันได้รับอิทธิพลจากความว่างเปล่าหรือไม่?";
					close;
					
					case 1:
					mes "เจ้ายังไม่เสร็จสิ้นในการค้นพบ.";
					mes "มันไม่ใช่น้ำแข็ง";
					mes "อุณหภูมิ แต่ถ้าเจ้าหนาวจัด";
					mes "มาหาข้า.";
					close;
					
					case 2:
					mes "เจ้ายังไม่เสร็จสิ้นในการค้นพบ.";
					mes "ข้าไม่คิดว่าพวกมันอยู่อีกด้านของกระจก";
					mes "แต่พวกมันอยู่บนช่องว่างมิติอื่น..."; 
					close;
				}
				case 2:
				switch(.@q) {
					case 0:
					case 1:
					case 2:
					mes "โอ้! ทำได้ดีมาก นั่นคือสิ่งที่ข้าต้องการ.";
					mes "น่าสนใจมาก...";
					mes "โอ้ ใช่แล้ว นี่คือรางวัลสำหรับการทำงานหนักของเจ้า.";
					mes "แล้วพบกันใหม่ ~ <3";
					getexp .abyss_bexp[.@q],.abyss_jexp[.@q];
					completequest .abyss_quest[.@q];
					erasequest .abyss_quest[.@q];
					setquest .abyss_delay[.@q];
					close;
				}
			}
			close;
	
		case 4:
		mes .NPCname$;
		mes "โอ้, วันนี้ข้ารู้สึกหนาว.";
		mes "Nillelm กำลังเศร้า!";
		break;
}
close;

OnInit:
	.NPCname$  = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.sb$ = "~";
	setarray .abyss_quest[0],8541,8544,8547;
	setarray .abyss_delay[0],8542,8545,8548;
	setarray .abyss_bexp[0],2000000,2000000,2000000;
	setarray .abyss_jexp[0],2000000,2000000,2000000;
end;
}


//==================================================
// mob spawn
//==================================================
gl_cas01_,0,0	monster	Corrupted Raydric	20367,30,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Corrupted Raydric Archer	20368,30,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Frozen Gargoyle	20369,20,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Corrupted Sting	20370,25,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Prison Breaker	20371,20,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Ice Ghost	20379,20,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Flame Ghost	20380,20,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0	monster	Corrupted Wanderer	20420,20,5000,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0,0,0	boss_monster	Corrupted Spider Queen	20421,1,7200000,600000,1,"#AbyssGlast_Drop::OnAGHMobDead"
gl_cas01_,0,0,0,0	boss_monster	Corrupted Dark Lord	20422,1,7200000,600000,1,"#AbyssGlast_Drop::OnAGHMobDead"

//==================================================
// Party Drop Script
//==================================================
gl_cas01_,0,0,0	script	#AbyssGlast_Drop	-1,{

OnAGHMobDead:
	if(getcharid(1)) {
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		
		for ( .@p = 0; .@p < $@partymembercount; .@p++)
			if(attachrid($@partymemberaid[.@p],$@partymembercid[.@p]) == 1) {
				if(strcharinfo(3) != strnpcinfo(4)) continue;
				if(rand(1,10000) >= (10000 - .chance)) 
					getitem .drop_id[rand(getarraysize(.drop_id))],1;
			}

	} else {
		if(rand(1,10000) >= (10000 - .chance)) 
			getitem .drop_id[rand(getarraysize(.drop_id))],1;
	}
end;

OnInit:
	setarray .drop_id[0],
		26138, // Hellfire Staff
		26139, // Icecle Staff
		26016, // Royal Knight's Lance
		21039, // Royal Knight's Broadsword
		28619; // Prisoner's Dairy
	// คูณโอกาสดรอปด้านล่างด้วย อัตราดรอปของเซิร์ฟเวอร์สำหรับอุปกรณ์
		// 1 = เปิด
		// 0 = ปิด
	.battleconf_drop = 0;
	// โอกาสดรอปทั่วไป
	.drop = 50; // 50 = 0.5%
	// การคำนวณ
	.chance = .drop * ( (.battleconf_drop)?( (getbattleflag("item_rate_equip")%100 > 0)? (getbattleflag("item_rate_equip")/100):1):1);
	end;
}