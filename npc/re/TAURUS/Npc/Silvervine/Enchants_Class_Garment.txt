morocc,153,78,6	script	Lace La Zard(Class)#cos_ect	4_WHITETIGER,{
	disable_items;
	mes "[Lace La Zard]";
	mes "Welcome to Fashion Stone!!";
	mes "My name is Lace La Zard who will make your 'Power Shoulder' dream come true.";
	next;
	mes "[Lace La Zard]";
	mes "Bring your ^ff0000Costume Garment^000000 and ^ff0000Garment Stone^000000, I will complete the perfect upgrade for you.!";
	mes "I found this formula while working on my own humble body upgrading!";
	next;
	if (select( "Apply ability.", "End Conversation." ) == 2) {
		mes "[Lace La Zard]";
		mes "Yes? Your want to look more?";
		close;
	}
	if (checkweight(1119,3) == 0) {
		mes "[Lace La Zard]";
		mes "Well... Your shoulder doesn't look possilbe for any more upgrade...!";
		mes "Don't be cruel to your shoulder!";
		next;
		mes "It seems you have too many items on you.";
		mes "Come back after clearing some inventory spaces.";
		close;
	}
	.@part = EQI_COSTUME_GARMENT;
	.@equip_id = getequipid(.@part);
	if (.@equip_id == -1) {
		mes "[Lace La Zard]";
		mes "Put the garment on your shoulder.";
		mes "I need to have a look at it to check for upgrading posibility..";
		close;
	}
	// costume garment list allowed (incomplete)

	.@equip_refine = getequiprefinerycnt(.@part);
	setarray .@card[0], getequipcardid(.@part, 0), getequipcardid(.@part, 1), getequipcardid(.@part, 2), getequipcardid(.@part, 3);

	mes "[Lace La Zard]";
	mes "Wow ^0000FF" + getitemname(.@equip_id) + "^000000!!! You got some sense there, aren't you?";
	mes "Great!! I'm fully ready for this upgrade!!";
	next;
	setarray .@data$[0],
		25448, 29463, "Rune_Knight",	// Rune_Knight
		1000296, 310257, "Rune_Knight_II",	// Rune_Knight_II
		25713, 29520, "Royal_Guard",	// Royal_Guard
		1000217, 310184, "Royal_Guard_II",	// Royal_Guard_II
		25717, 29524, "Assassin", // Assassin
		1000221, 310188, "Assassin II", // Assassin_II
		25493, 29480, "Shadow",	// Shadow
		25805, 29618, "Shadow_II",	//Shadow_II
		25416, 29431, "Ranger",	// Ranger
		1000011, 310003, "Ranger_II",	// Ranger_II
		29488, 29488, "Wanderer Minstrel",	// Wanderer_Minstrel_Stone
		1000304, 310265, "Wanderer Minstrel II",	// Wanderer_Minstrel_Stone_II
		25497, 29484, "Mechanic",	// Mechanic
		1000015, 310007, "Mechanic II",	// Mechanic_II
		25449, 29467, "Genetic",	// Genetic
		1000300, 310261, "Genetic_II",	// Genetic_II
		25456, 29471, "Warlock",	// Warlock
		1000213, 310180, "Warlock II",	// Warlock_II
		25420, 29435, "Sorcerer",	// Sorcerer
		25801, 29614, "Sorcerer II",	// Sorcerer_II
		25709, 29516, "ArcBishop",	// ArcBishopd
		1000019, 310011, "ArcBishopII",	// ArcBishop_II
		25412, 29426, "Sura",	// Sura
		25797, 29427, "Sura II",	// Sura_II
		25858, 29667, "Rebellion",	// Rebellion
		25854, 29663, "Oboro",	// Oboro
		25853, 29662, "Kagerou",	// Kagerou
		25849, 29658, "Star Emperor",	// Star Emperor
		25842, 29654, "Soul Reaper",	// Soul Reaper
		25862, 29671, "Summoner";	// Doram

	.@size = getarraysize(.@data$);
	for ( .@i = 0; .@i < .@size; .@i += 3 ) {
		.@id = atoi(.@data$[.@i]);
		.@menu$ += sprintf( "%d. %s (%d) left:", (.@j+1), getitemname(.@id), countitem(.@id) );
		.@j++;
	}
	.@menu$ += "End Conversation.";
	.@s = (select(.@menu$) - 1) * 3;
	if (.@s == .@size) {
		mes "[Lace La Zard]";
		mes "Yes? Your want to look more?";
		close;
	}
	.@id = atoi(.@data$[.@s]);
	.@enchant = atoi(.@data$[.@s+1]);
	if (getequipid(.@part) < 0 || countitem(.@id) < 1) {
		mes "[Lace La Zard]";
		mes "You must have both costume item and ability stone for this upgrading work.";
		close;
	}
	mes "^FF0000   !!! Caution !!!^000000";
	mes "^ff0000This upgrade can be failed.^000000";
	mes "^ff0000If the item already has extra ability, we will reset item's ability and give " + .@data$[.@s+2] + ".^000000";
	mes "^ff0000Are you sure about upgrading this item?^000000";
	next;
	if (select( "Yes.", "No." ) == 2) {
		mes "[Lace La Zard]";
		mes "Yes? Your want to look more?";
		close;
	}

	// anti-hack
	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
		close;

	delitem .@id, 1;
	delequip .@part;
	if (rand(100) < 51) {
		mes "[Lace La Zard]";
		mes "Beautiful!!";
		mes "Did I really made this myself!!!!";
		.@card[0] = .@enchant;
	}
	else {
		mes "[Lace La Zard]";
		mes "Oh no!";
		mes "Crash!!!!! I cannot show this failure to the World!!!";
		.@card[0] = 0;
	}
	getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
	close;
}