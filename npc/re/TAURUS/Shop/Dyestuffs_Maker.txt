//===== rAthena Script =======================================
//= Dyestuffs Maker
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0 Add Shop NPC with Barter Shop Src.
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================
// Dummy Shop
-	shop	Dyestuffs_Maker	-1,501:-1

legend_ro,88,121,5	script	Dyestuffs Maker	892,{
	disable_items;
	mes .NPCname$;
	mes "ยินดีต้อนรับเหล่านักผจญภัย";
	mes "ข้าคือช่างทำสีย้อมผ้า";
	mes "เจ้าสนใจไหมล่ะ?";
	callshop "Dyestuffs_Maker",1;
	npcshopattach "Dyestuffs_Maker";
	end;

OnBuyItem:
	if(getarraysize(@bought_nameid) > 1){
		mes .NPCname$;
		mes "ไม่สามารถซื้อสินค้า 2 รายการหรือมากกว่าในเวลาเดียวกัน.";
		close;
	}

	set .@shop_type$, "Dyestuffs_Maker";

	for(set .@l,0; .@l < getarraysize(getd("."+.@shop_type$+"$")); .@l++){

		explode(.@x$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

		if(@bought_nameid == atoi(.@x$[0])){
			explode(.@s$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

			for(set .@k,0; .@k < getarraysize(.@s$); set .@k,.@k + 1){
				set .@its[.@k], atoi(.@s$[.@k]);
			}

			mes .NPCname$;
			mes "[ รายการที่เลือก ] :";
			mes "ไอเท็ม: ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+"^000000";
			mes "จำนวน: ^0000FF"+@bought_quantity+"^000000";
			mes " ";
			mes "[ รายการที่ต้องการ ] :";
			for(set .@h,2; .@h < getarraysize(.@its); set .@h,.@h + 2){
				if(countitem(.@its[.@h]) >= .@its[.@h+1] * @bought_quantity){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ff"+getitemname(.@its[.@h])+""+((getitemslots(.@its[.@h]) > 0)?"[" + getitemslots(.@its[.@h]) + "]":"")+"^000000 ("+getd(".@color$")+""+countitem(.@its[.@h])+"/"+(.@its[.@h+1]*@bought_quantity)+"^000000)";
			}
			if(.@its[1] > 0){
				if(Zeny >= (.@its[1] * @bought_quantity)){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ffZeny^000000 ("+getd(".@color$")+""+Zeny+"/"+(.@its[1] * @bought_quantity)+"^000000)";
			}
			next;
			if(select("> ซื้อ ^0000ff"+getitemname(@bought_nameid)+"^000000 x "+@bought_quantity+"") == 1){
				if((.@its[1] * @bought_quantity) > Zeny){
						mes .NPCname$;
						mes "เจ้ามี Zeny ไม่เพียงพอ.";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
				}
				for(set .@j,2; .@j < getarraysize(.@its); set .@j,.@j + 2){
					if(countitem(.@its[.@j]) < (.@its[.@j+1]) * @bought_quantity){
						mes .NPCname$;
						mes "รายการที่ต้องการไม่เพียงพอ..";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
					}
				}
				for(set .@m,2; .@m < getarraysize(.@its); set .@m,.@m + 2){
					delitem .@its[.@m],(.@its[.@m+1] * @bought_quantity);
				}
				if(.@its[1] > 0){
					Zeny -= (.@its[1] * @bought_quantity);
				}
				specialeffect2 305;
				mes .NPCname$;
				mes "ยินดีด้วย! เจ้าได้รับ ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+" x "+@bought_quantity+"^000000.";

				getitem .@its[0], @bought_quantity;
				deletearray @bought_quantity, getarraysize(@bought_quantity);
				deletearray @bought_nameid, getarraysize(@bought_nameid);
				close;
			}
			break;
		}

	}


OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	waitingroom	"Color Quest",0;
	//- Remove the dummy item.
	npcshopdelitem "Dyestuffs_Maker",501,50;
	//- <ItemID>|<Zeny>|<ItemReq>|<ItemReqAmount>|<ItemIDReq1>|,....
	setarray .Dyestuffs_Maker$[0],
			"975|3000|507|30|973|1|713|1",//Scarlet Dyestuffs
			"976|3000|508|30|973|1|713|1",//Lemon Dyestuffs
			"978|3500|510|20|973|1|713|1",//Cobaltblue Dyestuffs
			"979|5000|510|5|511|20|508|20|974|1|973|1|713|1",//Darkgreen Dyestuffs
			"980|5000|507|20|508|20|974|1|973|1|713|1",//Orange Dyestuffs
			"981|5000|510|10|507|30|974|1|973|1|713|1",//Violet Dyestuffs
			"982|3000|509|30|973|1|713|1",//White Dyestuffs
			"983|7000|507|30|508|30|511|30|510|5|974|1|973|1|713|1",//Black Dyestuffs
			"973|3000|970|1|971|1|713|1",//Counteragent
			"974|4000|970|1|972|1|713|1";//Mixture

	if(!.itemload){
		//- Add item in the shop.
		for(set .@p,0; .@p < getarraysize(getvariableofnpc(.Dyestuffs_Maker$,"Dyestuffs Maker")); .@p++){
			explode(.@x$,getvariableofnpc(.Dyestuffs_Maker$[.@p],"Dyestuffs Maker"),"|");

			for(set .@k,0; .@k < getarraysize(.@x$); .@k++){
				set .@it[.@k], atoi(.@x$[.@k]);
			}
			npcshopadditem "Dyestuffs_Maker",.@it[0], 0;
		}
	}
	set .itemload, 1;
end;

OnTouch:
	deletearray @bought_quantity, getarraysize(@bought_quantity);
	deletearray @bought_nameid, getarraysize(@bought_nameid);
end;
}