legend_ro,88,129,5	script	Boosting weapon#1	868,{ 
callfunc "costumeaassc";

OnInit:
waitingroom " Boosting Weapon",0;
	end;
}

-	script	quest_costumeaassc	-1,{ 

function Add; function Chk; function Slot;

OnInit:

	freeloop(1);

	set .Announce,0;	// ประกาศเมื่อได้เสร็จสินเควส ? (1: ใช่ / 0: ไม่)
	set .ShowSlot,1;	// แสดง สล๊อต ไอเท็ม ? (2: แสดงทั้งหมด / 1: ถ้า สล๊อตน้อยกว่า > 0 / 0: never)
	set .ShowID,0;  	// แสดง รหัสไอเท็ม ? (1: ใช่ / 0: ไม่)
	set .ShowZeny,0;	// แสดงผล Zeny ถ้ามีค่าใช้จ่าย, ? (1: ใช่ / 0: ไม่)
	set .MaxStack,100;	// จำนวนสูงสุดของรายการเควส

// -----------------------------------------------------------
//  คะแนนพอยท์ ตัวแปร -- ตัวเลือกเควส.
//	setarray .Points$[0],"<ชื่อตัวแปร>","<การแสดงผล>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  รหัสร้านค้า -- การเพิ่มร้านค้า, คัดลอกข้อมูลที่ด้านล่างของไฟล์.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"^ff0000ใช้ Coin Booster Exchange แลก Item^0000CC";

// -----------------------------------------------------------
//  รายการเควส -- ไม่ใช้รายการรางวัลมากกว่าหนึ่งครั้ง!
//	Add(<รหัสร้านค้า>,<รหัสรางวัล>,<จำนวนรางวัล>,
//	    <ค่าใช้จ่าย Zeny>,<ค่าใช้จ่าย Cash Points>,
//	    <รหัสไอดี รายการที่ต้องหา>,<จำนวนของรายการที่ต้องหา>{,...});
// -----------------------------------------------------------

    Add(1,700001,1,20000000,1500,969,20);
    Add(1,600001,1,20000000,1500,969,20);
	Add(1,520000,1,20000000,1500,969,20);
	Add(1,610000,1,20000000,1500,969,20);
	Add(1,640000,1,20000000,1500,969,20);
	Add(1,590000,1,20000000,1500,969,20);
	Add(1,530000,1,20000000,1500,969,20);
	Add(1,500001,1,20000000,1500,969,20);
	Add(1,510001,1,20000000,1500,969,20);
	Add(1,540000,1,20000000,1500,969,20);
	Add(1,560000,1,20000000,1500,969,20);
	Add(1,570000,1,20000000,1500,969,20);
	Add(1,580000,1,20000000,1500,969,20);
	Add(1,830000,1,20000000,1500,969,20);
	Add(1,540001,1,20000000,1500,969,20);
	Add(1,550001,1,20000000,1500,969,20);
	Add(1,550002,1,20000000,1500,969,20);
	Add(1,510002,1,20000000,1500,969,20);
	Add(1,450001,1,20000000,1500,969,20);
	Add(1,480000,1,20000000,1500,969,20);
	Add(1,470000,1,20000000,1500,969,20);
	Add(1,450004,1,20000000,1500,969,20);
	Add(1,480003,1,20000000,1500,969,20);
	Add(1,470003,1,20000000,1500,969,20);
	Add(1,480001,1,20000000,1500,969,20);
	Add(1,470001,1,20000000,1500,969,20);
	Add(1,450003,1,20000000,1500,969,20);
	Add(1,480002,1,20000000,1500,969,20);
	Add(1,470002,1,20000000,1500,969,20);
	Add(1,490004,1,20000000,1500,969,20);
	Add(1,490007,1,20000000,1500,969,20);
	Add(1,490005,1,20000000,1500,969,20);
	Add(1,490006,1,20000000,1500,969,20);

// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "costumeaassc"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"มีข้อผิดพลาดที่เกิดขึ้น.";
		end;
	}
	dispbottom "กรุณาเลือกรายการในเวลาที่กำหนด.";
	callshop "costumeaassc"+@shop_index,1;
	npcshopattach "costumeaassc"+@shop_index;
	end;

OnBuyItem:

	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"คุณไม่สามารถซื้อ "+getitemname(.@q[0])+" ครั้งละ หลายรายการ.";
		end;
	}
	mes "[Quest Shop]";
	mes "รางวัล: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "รายการที่ต้องหา:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ แลก ^0055FF"+getitemname(.@q[0])+"^000000 ทันที:"+((.@preview && !@qe[7])?" ~ ทดลองสวมใส่...":"")+": ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "คุณไม่มีรายการไอเท็มที่ต้องหา กรุณาตรวจสอบอีกครั้ง.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000คุณต้องลดน้ำหนัก "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" เพื่อรองรับไอเท็มนี้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" ได้สร้าง "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "costumeaassc"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	costumeaassc	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_costumeaassc::OnMenu";
	end;
}


// Dummy ข้อมูลร้าค้า -- คัดลอกตามความจำเป็น
//============================================================

-	shop	costumeaassc1	-1,909:-1
