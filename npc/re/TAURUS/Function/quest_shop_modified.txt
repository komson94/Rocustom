//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//= 1.7 Added Success rates - by Mabuhay
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "ill_qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
//prontera,164,203,6	script	Quest Shop#1	998,{ callfunc "ill_qshop"; }
// prontera,165,203,6	script	Quest Shop#2	998,{ callfunc "ill_qshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	ill_qshops	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// ประกาศเมื่อสร้างสำเร็จ? (1: เปิด / 0: ปิด)
	set .ShowSlot,1;	// แสดง ช่อง ของไอเทม? (2: อุปกรณ์ทั้งหมด / 1: ถ้า ช่อง > 0 / 0: ไม่แสดง)
	set .ShowID,1;  	// แสดง ID ของไอเทม? (1: เปิด / 0: ปิด)
	set .ShowZeny,1;	// แสดง ค่าใช้จ่าย Zeny ถ้ามี? (1: เปิด / 0: ปิด)
	set .MaxStack,100;	// จำนวนสูงสุดที่ซื้อในครั้งเดียว.
	set .SuccessRate,1;	// เปิดใช้งานอัตราความสำเร็จ? ( 1: เปิด / 0: ปิด)

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Arrows Packing",	// Shop Named 1
		"Weapons",		// Shop Named 2
		"Other";		// Shop Named 3

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,<success rate>
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------
// Add(<shop ID>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,<success rate>,<required item ID>,<required item amount>{,...});
// Illusion of Twins
	Add(1,450182,1,0,0,80,2387,1,1000517,125,1000514,55,25271,100);
	Add(1,470066,1,0,0,80,2440,1,1000515,75,1000511,125,25271,100);
	Add(1,550030,1,0,0,80,1636,1,1000516,75,1000515,75,25271,135);
	Add(1,550031,1,0,0,80,2005,1,1000518,15,1000511,125,1000515,75,25271,150);
	Add(1,530015,1,0,0,80,1414,1,1000514,55,1000510,125,25271,150);
	Add(1,500030,1,0,0,80,1137,1,1000516,75,1000513,75,25271,135);
	Add(1,620010,1,0,0,80,1370,1,1000513,75,1000511,75,25271,150);
	Add(1,510034,1,0,0,80,13062,1,1000516,75,1000517,125,25271,135);
	Add(1,460017,1,0,0,80,2101,1,1000510,75,1000517,125,25271,100);
	Add(1,460018,1,0,0,80,2146,1,1000512,55,1000510,125,25271,100);
	Add(1,490120,1,0,0,80,2744,1,1000513,75,1000510,125,25271,120);
	Add(1,490121,1,0,0,80,2935,1,1000512,55,1000517,125,25271,120);

// Shop 2
	Add(2,1224,1,0,0,25,7297,30,969,10,999,50,714,10);
	Add(2,1225,1,0,0,50,7292,30,969,10,999,50,714,10);

// Shop 3
	Add(3,531,1,3,0,50,512,1,713,1);
	Add(3,532,1,3,0,50,513,1,713,1);
	Add(3,533,1,3,0,50,514,1,713,1);
	Add(3,534,1,3,0,50,515,1,713,1);

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "ill_qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"มีข้อผิดพลาดเกิดขึ้น.";
		end;
	}
	dispbottom "เลือกได้แค่ครั้งละหนึ่งชิ้น.";
	callshop "ill_qshop"+@shop_index,1;
	npcshopattach "ill_qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, SuccessRate, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"เจ้าไม่สามารถสร้าง "+getitemname(.@q[0])+" ได้มากกว่านี้.";
		end;
	}
	mes "[Quest Shop]";
	mes "สิ่งที่ต้องการสร้าง: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	if (.SuccessRate) mes "โอกาสสำเร็จ: ^ff0000"+ .@q[6] +"^000000%";
	mes "ของที่ใช้สร้าง:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[7]) for(set .@i,7; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ สร้าง ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ ลองสวมใส่...":"")+": ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "เจ้าขาดหรือมีของไม่พอแลก.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000เจ้าต้องมีพื้นที่น้ำหนัก "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" เป็นอย่างน้อยในการสร้างของครั้งนี้.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[7]) for(set .@i,7; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			// If success rate setting is on, this happens if it fails
			if ( .SuccessRate && .@q[6] <= rand(100) ) { // on fail
				mes "[Quest Shop]";
				mes "โชคไม่ดี! การสร้างไอเท็มล้มเหลว!";
				close;
			}
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" ได้สร้าง "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "รางวัลเควส #"+getarg(1)+" ไม่ถูกต้อง (ข้าม).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4),getarg(5);
	for(set .@i,6; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "ข้อกำหนดเควส #"+getarg(.@i)+" ไม่ถูกต้อง (ข้าม).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "ill_qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	ill_qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "ill_qshops::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	ill_qshop1	-1,909:-1
-	shop	ill_qshop2	-1,909:-1
-	shop	ill_qshop3	-1,909:-1
-	shop	ill_qshop4	-1,909:-1
-	shop	ill_qshop5	-1,909:-1
