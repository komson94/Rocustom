  // Costume enchant (top, mid, low)
  askald,71,102,3	script	Effect Enchant#cos_ect	4_WHITETIGER,{
  	disable_items;
  	mes "[Aver De Dosh]";
  	mes "Welcome to Fashion Stone!!";
  	next;
  	mes "[Aver De Dosh]";
  	mes "We take various costume items.";
  	next;
  	mes "[Aver De Dosh]";
  	mes "If you want cool styling for your costume Hat, please come to me 'Aver De Dosh'!";
  	mes "Don't forget to bring ^ff0000your own costume Hat and costume stones^000000~";
  	next;
  	if (select( "Apply ability.", "End Conversation." ) == 2) {
  		mes "[Aver De Dosh]";
  		mes "Please check our other upgrading services~";
  		close;
  	}
  	if (checkweight(1119,3) == 0) {
  		mes "[Aver De Dosh]";
  		mes "Kiaaaaach!!!";
  		mes "I hate bulging bags!";
  		mes "No matter how cool your hat looks, that fat bag of yours is a fashion terror you know!";
  		next;
  		mes "[Aver De Dosh]";
  		mes "It seems you have too many items on you.";
  		mes "Come back after clearing some inventory spaces.";
  		close;
  	}
  	.@eq_top = getequipid(EQI_COSTUME_HEAD_TOP);
  	.@eq_mid = getequipid(EQI_COSTUME_HEAD_MID);
  	.@eq_low = getequipid(EQI_COSTUME_HEAD_LOW);
  
  	if (.@eq_top == -1 && .@eq_mid == -1 && .@eq_low == -1) {
  		mes "[Aver De Dosh]";
  		mes "You must wear item you want to add ability.";
  		mes "Then, I will be able to help you out.";
  		close;
  	}
  	if (.@eq_top > -1) {
  		.@part = EQI_COSTUME_HEAD_TOP;
  		.@equip_id = .@eq_top;
  	}
  	else if (.@eq_mid > -1) {
  		.@part = EQI_COSTUME_HEAD_MID;
  		.@equip_id = .@eq_mid;
  	}
  	else if (.@eq_low > -1) {
  		.@part = EQI_COSTUME_HEAD_LOW;
  		.@equip_id = .@eq_low;
  	}
    
  	.@location = getiteminfo(.@equip_id, 5);
  	.@equip_refine = getequiprefinerycnt(.@part);
  	setarray .@card[0], getequipcardid(.@part, 0), getequipcardid(.@part, 1), getequipcardid(.@part, 2), getequipcardid(.@part, 3);
  
  	mes "[Aver De Dosh]";
  	mes "Wow~ This ^0000FF" + getitemname(.@equip_id) + "^000000!!! looks great on you!!";
  	mes "What kind of stone do you want to use for the upgrade?";
  	next;
  	if (.@part == EQI_COSTUME_HEAD_TOP)
  		.@slot = select( "Upper", "Middle", "Lower", "Cancel" ) - 1;	// the slot enchanted changes according to the location
  	else if (.@part == EQI_COSTUME_HEAD_MID)
  		.@slot = select( "", "Middle", "Lower", "Cancel" ) - 1;
  	else if (.@part == EQI_COSTUME_HEAD_LOW)
  		.@slot = 2;
  
  	switch(.@slot) {
  	case 0:
  		if (!(.@location & 1024)) {
  			mes "[Aver De Dosh]";
  			mes "The costume can't be enhanced at this location.";	// custom
  			close;
  		}
  		setarray .@stone_id[0],
  			25058, 29041,	// Twinkle Effect(Upper)
			25206,	29142;	// Electric Effect(Upper)
			

			
  		break;
  	case 1:
  		if (!(.@location & 2048)) {
  			mes "[Aver De Dosh]";
  			mes "The costume can't be enhanced at this location.";	// custom
  			close;
  		}
  		setarray .@stone_id[0],
  			25059, 29040,	// Ghost Effect
			25136, 29142,	// Electric Effect
			25138, 29041,	// Twinkle Effect
			25176, 29160,	// Blue Aura Effect
			25177, 29162,	// Shadow Effect
			25178, 29161,	// Pink Glow Effect
			25224, 29224,	// White Body Effect
			25225, 29226;	// Crimson Wave Effect

  		break;
  	case 2:
  		if (!(.@location & 4096)) {
  			mes "[Aver De Dosh]";
  			mes "The costume can't be enhanced at this location.";	// custom
  			close;
  		}
  		setarray .@stone_id[0],
  			25205, 29144,	// Shrink Effect
			25137, 29143,	// Green Flare
			25226, 29225;	// Water Field Effect


  		break;
  	default:
  		mes "[Aver De Dosh]";
  		mes "Please check our other upgrading services~";
  		close;
  	}
  	.@size = getarraysize(.@stone_id);
  	for ( .@i = 0; .@i < .@size; .@i += 2 )
  		.@menu$ += sprintf( "%d. %s [%d] left:", (.@i/2)+1, getitemname(.@stone_id[.@i]), countitem(.@stone_id[.@i]) );
  	.@s = (select(.@menu$) - 1) * 2;
  	.@stone = .@stone_id[.@s];
  	.@enchant = .@stone_id[.@s+1];
  
  	if (getequipid(.@part) < 0 || countitem(.@stone) < 1) {
  		mes "[Aver De Dosh]";
  		mes "Hmm? Think you are lack of ingredients.";
  		mes "You must have both costume item and ability stone for this upgrading work.";
  		close;
  	}
  	mes "^FF0000   !!! Caution !!!^000000";
  	mes "^FF0000This upgrade can be failed.^000000";
  	mes "^FF0000If the item already has extra ability,^000000";
  	mes "^FF0000we will reset item's ability.^000000";
  	mes "^FF0000Are you sure about upgrading this item?^000000";
  	next;
  	if (select( "Yes.", "No." ) == 2) {
  		mes "[Aver De Dosh]";
  		mes "Please check our other upgrading services~";
  		close;
  	}
  	delitem .@stone, 1;
  
  	// anti-hack
  	if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
  		close;
  
  	delequip .@part;
  	if (rand(1,100) < 101) {
  		mes "[Aver De Dosh]";
  		mes "Wow... Elegance!!";
  		mes "This is perfect... Wow!";
  		.@card[3] = .@enchant;
  	}
  	else {
  		mes "[Aver De Dosh]";
  		mes "History is known for its failure...";
  		mes "History of Fashion also is...";
  		mes "Sorry...to disappoint you..~ LoL.";
  		.@card[3] = 0;
  	}
  	getitem2 .@equip_id,1,1,.@equip_refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
  	close;
  }