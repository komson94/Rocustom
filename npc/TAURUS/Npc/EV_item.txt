
louyang,244,103,4	script	EventTeam#01	873,{
set .@mes$,"^FF0000[ Event Team ]^000000";
if(getgmlevel()>=60)
	goto PLZ_GM;
else
	goto PLZ_Main;
end;

PLZ_GM:
	mes .@mes$;
	mes "- กิจกรรมแจกของ สำหรับ GM";
	mes "- ท่านต้องการให้ข้าช่วยอะไรหรือ";
	next;
	menu "- ย้ายตำแหน่ง",PLZ_Move,"- เซทกิจกรรม",PLZ_Set,"- เปิดกิจกรรม",PLZ_Open,"- ปิดกิจกรรม",PLZ_Close;
end;

PLZ_Move:
	mes .@mes$;
	mes "- กรอกตำแหน่งที่ต้องการย้าย";
	mes "- กรอกแกน X ก่อน ตามด้วยแกน Y";
	next;
	input $EVT_X;
	input $EVT_Y;
	movenpc "EventTeam#01",$EVT_X,$EVT_Y;
	mes .@mes$;
	mes "- ย้ายตำแหน่งให้เรียบร้อยแล้ว";
	mes "- ตำแหน่งใหม่ [ "+$EVT_X+" | "+$EVT_Y+" ]";
	close;
end;

PLZ_Set:
	mes .@mes$;
	mes "- ใส่รหัส item ที่ต้องการแจก";
	input $EVT_item;
	next;
	mes .@mes$;
	mes "- ใส่จำนวน item ที่ต้องการแจก";
	input $EVT_amount1;
	next;
	mes .@mes$;
	mes " ";
	mes "- ใส่รหัส item ที่ต้องใช้แลก";
	input $EVT_itemid2;
	next;
	mes .@mes$;
	mes " ";
	mes " ";
	mes "- จำนวน item ที่ต้องใช้แลก";
	input $EVT_amount;
	next;
	mes .@mes$;
	mes " ";
	mes " ";
	mes " ";
	mes "- จำนวนครั้งที่ให้แลกได้";
	input $EVT_count;
	next;
	mes .@mes$;
	mes "- แจก "+getitemname($EVT_item);
	mes "- จำนวน "+$EVT_amount1;
	mes "- โดยต้องใช้ "+getitemname($EVT_itemid2);
	mes "- จำนวน "+$EVT_amount;
	mes "- แลกได้ทั้งหมด "+$EVT_count;
	next;
	if(select("- แก้ไขใหม่","- กลับสู่เมนู")==1)
		goto PLZ_Set;
	goto PLZ_GM;
end;

PLZ_Open:
	mes .@mes$;
	mes "- แจก "+getitemname($EVT_item);
	mes "- จำนวน "+$EVT_amount1;
	mes "- โดยต้องใช้ "+getitemname($EVT_itemid2);
	mes "- จำนวน "+$EVT_amount;
	mes "- แลกได้ทั้งหมด "+$EVT_count;
	next;
	if(select("- เริ่มกิจกรรมได้ทันที","- กลับสู่เมนู")==2)
		goto PLZ_GM;
	donpcevent "EventTeam#01::OnGo";
	close;		
end;

OnGo:
	announce "[ Event ] : ขณะนี้ กิจกรรมแลกของได้เริ่มขึ้นแล้ว ",0;
	sleep2 5000;
	announce "[ Event ] : รางวัลคือ "+getitemname($EVT_item)+" จำนวน "+$EVT_amount1+" ชิ้น",0;
	sleep2 5000;
	announce "[ Event ] : สิ่งที่ต้องใช้คือ "+getitemname($EVT_itemid2)+" จำนวน "+$EVT_amount+" ชิ้น",0;
	sleep2 5000;
	announce "[ Event ] : รับรางวัลได้เพียง "+$EVT_count+" ท่านเท่านั้น",0;
	set $EVT_Start,1;
	set $EVT_Time,$EVT_Time+1;
end;

PLZ_Close:
	mes .@mes$;
	mes "- คุณต้องการจะปิดกิจกรรม";
	next;
	if(select("- ยังก่อน","- ปิดเลย")==1)
		close;
	announce "[ Event Team ] : ขณะนี้ปิดกิจกรรมแจกของแล้ว !!",0;
	set $EVT_Start,0;
	close;


PLZ_Main:
	mes .@mes$;
	if($EVT_Start==0){
		mes "- ขณะนี้กิจกรรมยังไม่เปิดให้ใช้งาน";
		close;
	}
	//if ((BaseLevel < 30)) {
	//		mes " ข้าเสียใจ ต้องมี Level 30 ขึ้นไปถึงจะร่วมกิจกรรมได้";
	//		close;
	//}
	if (BaseJob == Job_Novice) {

			mes " ข้าเสียใจ แต่ Novice ไม่สามารถร่วมกิจกรรมได้";

			close;
	}
	mes "- ขณะนี้แลกได้อีก "+$EVT_count+" ท่าน";
	mes "- ของที่ได้รับ  "+getitemname($EVT_item);
	mes "- จำนวน "+$EVT_amount1+" ชิ้น";
	mes "- ต้องใช้"+getitemname($EVT_itemid2);
	mes "- จำนวน "+$EVT_amount+" ชิ้น";
	next;
	if(select("- แลกเลย","- ยังก่อน")==2)
		close;
	if(countitem($EVT_itemid2)<$EVT_amount){
		mes .@mes$;
		mes "- คุณมีของไม่พอตามจำนวน";
		close;
	}
	else if(#EVT_Time==$EVT_Time){
		mes .@mes$;
		mes "- 1 ID แลกได้เพียง 1 ครั้งนะท่าน ";
		close;
	}
	else if($EVT_Start!=0){
		delitem $EVT_itemid2,$EVT_amount;
		getitem $EVT_item,$EVT_amount1;
		announce "[ "+strcharinfo(0)+" ] ได้แลก Item จากกิจกรรมไปแล้ว",0;
		set $EVT_count,$EVT_count-1;
		set #EVT_Time,$EVT_Time;
		if($EVT_count<=0){
			announce "[ Event Team ] : กิจกรรมแลกของปิดลง เนื่องจากมีผู้เล่นนำของมาแลก ครบตามจำนวนแล้ว !!",0;
			set $EVT_Start,0;
		}
		close;
	}
end;

OnInit:
waitingroom "กิจกรรมแลกคอสตูม สงกราน",0; 
end; 

end;
}

