prontera,163,154,4	script	รับของรางวัลชดเชย	864,{
OnScripts:
	if ( getgmlevel() == .Access )
	{
		switch(prompt("~ ตั้งค่าของรางวัลชดเชย:~ เมนูคนเล่น:~ ยกเลิกการสนทนา"))
		{
			case 1:
				mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
				mes "กรุณาใส่รหัสไอเท็ม";
				mes "^FF0000ตัวอย่าง ItemID = 501 ^000000";
				mes "^FF0000รางวัลจึงเป็น Red Potion ^000000";
				next;
				input .@Reward;
				mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
				if ( getitemname(.@Reward) == "" || getitemname(.@Reward) == "null" )
				{
					mes "ขออภัยด้วยค่ะ ?.";
					mes "รหัสไอเท็มไม่มีอยู่ในฐานข้อมูล";
					end;
				}
				set .Itemid,.@Reward;
				mes "โปรดระบุจำนวนไอเท็ม";
				mes "ที่ต้องการแจกผู้เล่นด้วยค่ะ";
				next;
				input .@RAmount;
				mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
				if ( .@RAmount <= 0 || .@RAmount >= 32000 )
				{
					mes "^FF0000คุณกรอกจำนวนไม่ถูกต้อง^000000";
					mes "^FF0000กรอกขั้นต่ำคือ 1 และ ไม่เกิน 32K^000000";
					set .@RAmount,1;
				}
				set .Amount,.@RAmount;
				mes "การตั้งค่าของรางวัลเสร็จสมบูรณ์";
				mes "ของรางวัลปัจจุบันคือ "+ getitemname(.Itemid) +" จำนวน "+ .Amount +"";
				set Accept,0;
				end;

			case 2:
				goto PLAYMAIN;
				end;
				
			default:
				end;
		}
		end;
	}
	PLAYMAIN:
		mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
		mes "สวัสดีค่ะ เป็นไงบ้างค่ะ !";
		mes "ฉันเป็นตัวแทนของ ^FF0000Game^000000 ^0000FFMaster^000000";
		mes "หน้าที่ฉันค่อยแจกของรางวัลชดเชย";
		mes "คุณต้องการรับของรางวัลชดเชยหรือไม่";
		next;
		switch(prompt("~ แน่นอน ฉันชอบของฟรี !:~ เปล่า,แค่แวะมาเฉยๆ"))
		{
			case 1:
				if ( getitemname(.Itemid) == "" || getitemname(.Itemid) == "null" )
				{
					mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
					mes "GM ยังไม่ได้ตั้งค่าของรางวัลชดเชย";
					mes "โปรดติดตามข่าวสารจากทีมงานอีกครั้ง";
					end;
				}
				else if ( Accept == 1 )
				{
					mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
					mes "คุณได้รับของรางวัลไปก่อนหน้านี้แล้ว";
					mes "^0000FF1 ตัวละคร รับได้ 1 ครั้งเท่านั้น^000000";
					mes "ลาก่อน บ๊ะบายค่ะ !!";
					end;
				}
				else
				{
					getitem .Itemid,.Amount;
					set Accept,1;
					end;
				}
				
			default: 
			end;
		}
		end;

OnInit:
	.Access = 99;
	waitingroom "รับของรางวัลชดเชย", 0;
	end;
}

-	script	core_main	FAKE_NPC,{
	
OnCore:
		mes "[ ผู้ดูแลประจำเซิฟเวอร์ ]";
		mes "สวัสดีท่าน GM Master ^FF0000"+ strcharinfo(0) +"^000000";
		mes "เป็นไงบ้างสบายดีหรือป่าว";
		mes "วันนี้ต้องให้ฉันช่วยเหลือไรมัยค่ะ";
		next;
		switch(prompt("~ เปิด npc รับของรางวัลชดเชย:~ ปิด npc รับของรางวัลชดเชย"))
		{
			case 1:
				enablenpc "รับของรางวัลชดเชย";
				end;
				
			case 2:
				disablenpc "รับของรางวัลชดเชย";
				end;
		}
		end;
		
OnInit:
	bindatcmd "rewardconfig", strnpcinfo(3) +"::OnCore",99,99;
	end;
}

prontera,163,158,4	script	MailBox001	888,{
	if ( getgmlevel() == 99 )
	{
		mes "[ MailBox001 ]";
		mes "สวัสดีท่าน GM Master ^FF0000"+ strcharinfo(0) +"^000000";
		mes "เป็นไงบ้างสบายดีหรือป่าว";
		mes "แล้วคุณมีอะไรให้ฉันช่วยเหลือมัย ?";
		next;
		switch(prompt("~ Menu ^FF0000สำหรับผู้ดูแลระบบ^000000 :~ Menu สำหรับผู้เล่นรับของรางวัล"))
		{	
			case 1:

				switch(select("~ ส่งรางวัลกิจกรรม","~ ตรวจสอบรายการรางวัล","~ ยกเลิก"))
				{
					case 1:
						mes "[ MailBox001 ]";
						mes "กรุณากรอก User ID ของผู้เล่น";
						mes "0 = ยกเลิก";
						set .@userid$,"";
						input .@userid$;
						if(.@userid$ != "0")
						{

							query_sql "SELECT `account_id` FROM `login` WHERE `userid` LIKE '"+escape_sql(.@userid$)+"'",.@aid;
							if(getarraysize(.@aid) > 1)
							{
								mes "^FF0000ERROR: ตรวจพบ User ID ผู้เล่นที่มากกว่า 1 รายการ^000000";
							}
							else if(getarraysize(.@aid) == 0)
							{
								mes "USER ID: "+.@userid$;
								mes "^FF0000ERROR: ไม่พบ User ID ผู้เล่นในฐานข้อมูล^000000";
							}
							else
							{

								set .@account_id,.@aid;
								set .@aid,0;
								mes " ";
								mes "USER ID: ^000088"+.@userid$+"^000000";
								mes "Account ID: ^000088"+.@account_id+"^000000";
								next;
								mes "[ MailBox001 ]";
								mes "USER ID: ^000088"+.@userid$+"^000000";
								mes "Account ID: ^000088"+.@account_id+"^000000";
								mes " ";
								mes "กรุณาใส่ ITEM ID ที่ต้องการจะส่ง";
								input .@item;
								if(.@item)
								{
									mes "กรุณาใส่จำนวน ITEM ที่ต้องการจะส่ง";
									input .@amount,0,30000;
									if(.@amount)
									{
										next;
										mes "[ MailBox001 ]";
										mes "USER ID: ^000088"+.@userid$+"^000000";
										mes "Account ID: ^000088"+.@account_id+"^000000";
										mes "รายการไอเท็ม > ^000088"+getitemname(.@item)+"^000000";
										mes "จำนวนไอเท็ม > ^000088"+.@amount+" รายการ^000000";
										if(select("~ ยืนยัน","~ ยกเลิก")==1)
										{
											query_sql "INSERT INTO `reward_npc` VALUES ('','"+escape_sql(.@userid$)+"','"+.@account_id+"','"+.@item+"','"+.@amount+"','0',NULL)";
											next;
											mes "[ MailBox001 ]";
											mes "^009900ทำการบันทึกลงฐานข้อมูลเรียบร้อยแล้ว^000000";
											set .@account_id,0;
											set .@item,0;
											set .@amount,0;
										}
									}
								}
							}
							deletearray .@aid[0],getarraysize(.@aid);
						}
						break;
						
					case 2:
					
						set .@choice,select("~ เช็คไอดีผู้เล่นทั้งหมดที่ยังไม่ได้รับของรางวัล","~ เช็คไอดีผู้เล่นที่ยังไม่ได้รับของรางวัล");
						if(.@choice == 2)
						{
							mes "[ MailBox001 ]";
							mes "กรุณากรอก User ID ที่ต้องการจะตรวจสอบ";
							mes "0 = ยกเลิก";
							set .@userid$,"";
							input .@userid$;
							if(.@userid$ != "0")
							{
								query_sql "SELECT `account_id` FROM `login` WHERE `userid` LIKE '"+escape_sql(.@userid$)+"'",.@aid;
								if(getarraysize(.@aid) > 1)
								{
									mes "^FF0000ERROR: ตรวจพบ User ID ผู้เล่นที่มากกว่า 1 รายการ^000000";
								}
								else if(getarraysize(.@aid) == 0)
								{
									mes "^FF0000ERROR: ไม่พบ User ID ผู้เล่นในฐานข้อมูล^000000";
								}
								else
								{
									if(select("~ รายการที่ยังไม่ได้รับ","~ รายการที่รับไปแล้ว")==1)
									{
										set .@status,0;
									}
									else
									{
										set .@status,1;
									}
									
									next;
									query_sql "SELECT `item_id`,`rew_amount` FROM `reward_npc` WHERE `account_id` = '"+.@aid+"' AND `rew_status` = '"+.@status+"' ORDER BY `id` DESC LIMIT "+.query_limit,.@rewlist,.@amtlist;
									mes "[ MailBox001 ]";
									mes "^009900USERID: "+.@userid$+"^000000";
									mes "=======================";
									if(getarraysize(.@rewlist))
									{
										for(set .@i,0 ; .@i < getarraysize(.@rewlist) ; set .@i,.@i+1)
										{
											mes "^000088"+getitemname(.@rewlist[.@i])+"^000000 จำนวน "+.@amtlist[.@i]+" รายการ";
										}
										mes "=======================";
										deletearray .@rewlist[0],getarraysize(.@rewlist);
										deletearray .@amtlist[0],getarraysize(.@amtlist);
									}
									else
									{
										mes "^FF0000ไม่พบรายการบนฐานข้อมูล^000000";
									}
								}
								deletearray .@aid[0],getarraysize(.@aid);
							}
						}
						else
						{
						VIEW_AGAIN:
							query_sql "SELECT DISTINCT(`userid`) FROM `reward_npc` WHERE `rew_status` = '0' LIMIT "+.query_limit,.@userlist$;
							if(getarraysize(.@userlist$))
							{
								set .@menu$,"";
								for(set .@i,0 ; .@i < getarraysize(.@userlist$) ; set .@i,.@i+1)
								{
									set .@menu$,.@menu$+""+.@userlist$[.@i]+":";
								}
								set .@choice,select(.@menu$+"^FF8800กลับเมนูหลัก^000000");
								
								if(.@choice <= getarraysize(.@userlist$))
								{
									set .@user$,.@userlist$[.@choice-1];
									query_sql "SELECT `item_id`,`rew_amount` FROM `reward_npc` WHERE `userid` LIKE '"+.@user$+"' AND `rew_status` = 0   ORDER BY `id` DESC LIMIT "+.query_limit,.@item,.@amount;
									
									if(getarraysize(.@item) && getarraysize(.@item) == getarraysize(.@amount))
									{
										mes "[ MailBox001 ]";
										mes "USERID: ^000088"+.@user$+"^000000";
										mes "=======================";
										for(set .@i,0 ; .@i < getarraysize(.@item) ; set .@i,.@i+1)
										{
											mes getitemname(.@item[.@i])+" จำนวน "+.@amount[.@i]+" รายการ";
										}
										mes "=======================";
										deletearray .@item[0],getarraysize(.@item);
										deletearray .@amount[0],getarraysize(.@amount);
										next;
										goto VIEW_AGAIN;
									}
								}
							}
							else
							{
								mes "[ MailBox001 ]";
								mes "^FF0000ไม่พบรายการบนฐานข้อมูล^000000";
							}	
						}
						break;
						
					default:
						mes "[ MailBox001 ]";
						mes "ไว้พบกันใหม่";
						close;
						break;
				}
				end;
			
			case 2:
				goto PLAYER;
				end;
		}
		end;
	}
	else
	{
PLAYER:
		mes "[ MailBox001 ]";
		mes "สวัสดีคุณ ^0000FF"+ strcharinfo(0) +"^000000";
		mes "เป็นไงบ้างสบายดีหรือป่าว";
		mes "แล้วคุณมีอะไรให้ฉันช่วยเหลือมัย ?";
		next;
		switch(prompt("~ ใช่..ฉันมารับของรางวัลเท่านั้นล่ะ..!"))
		{	
			case 1:
				deletearray .@id[.@j],getarraysize(.@id);
				query_sql "SELECT `id`,`item_id`,`rew_amount` FROM `reward_npc` WHERE `account_id` = '"+getcharid(3)+"' AND `rew_status` = '0' LIMIT "+.query_limit+"",.@id,.@item,.@amount;
				if(.@id && getarraysize(.@id))
				{
				RECEIVE_REWARD:
					mes "[ MailBox001 ]";
					mes "ขอแสดงความยินดีด้วยค่ะ";
					mes "คุณ ^0000FF"+ strcharinfo(0) +"^000000 มีรายการไอเท็มส่งถึงคุณ";
					mes "ส่งจาก Game Master โชคดีค่ะ ?";
					set .@menu$,"";
					for( set .@i,0 ; .@i < getarraysize(.@id) ; set .@i,.@i+1 )
					{
						set .@menu$,.@menu$+"^ff0000"+getitemname(.@item[.@i])+"^000000 จำนวน "+.@amount[.@i]+" ea:";
					}
					set .@menu,select(.@menu$+"ยังไม่รับตอนนี้");
					if(.@menu <= getarraysize(.@id))
					{
						set .@j,.@menu-1;
						next;
						query_sql "UPDATE `reward_npc` SET `rew_status` = '1', `received` = '"+gettimestr("%Y-%m-%d %H:%M:%S",21)+"' WHERE `id` = '"+.@id[.@j]+"' AND `item_id` = "+.@item[.@j]+" AND `rew_amount` = "+.@amount[.@j]+" LIMIT 1";
						getitem .@item[.@j],.@amount[.@j];
						mes "[ MailBox001 ]";
						mes "ได้รับ "+getitemname(.@item[.@j]);
						mes "จำนวน "+.@amount[.@j]+" รายการ";
						//query_sql ("DELETE FROM `reward_npc` WHERE `id` = "+.@id[.@j]+"");
						deletearray .@id[.@j],1;
						deletearray .@item[.@j],1;
						deletearray .@amount[.@j],1;
						if(getarraysize(.@id))
						{
							next;
							goto RECEIVE_REWARD;
						}
					}
				}
				else
				{
					mes "[ MailBox001 ]";
					mes "เสียใจด้วยค่ะ...!";
					mes "คุณ ^0000FF"+ strcharinfo(0) +"^000000 ไม่มีรายการไอเท็มส่งถึงคุณ";
				}
				close2;
				end;
		}
	}
	end;

OnInit:
	waitingroom "รับเรเวล CBT 200", 0;
	query_sql "CREATE TABLE IF NOT EXISTS `reward_npc` ( `id` INT( 5 ) UNSIGNED NOT NULL AUTO_INCREMENT PRIMARY KEY ,`userid` VARCHAR( 23 ) NOT NULL, `account_id` INT( 11 ) UNSIGNED NOT NULL , `item_id` INT( 5 ) UNSIGNED NOT NULL , `rew_amount` INT( 5 ) UNSIGNED NOT NULL , `rew_status` BOOL NOT NULL , `received` DATETIME default NULL) ENGINE = MYISAM";	
	set .query_limit,128;
	end;
}
