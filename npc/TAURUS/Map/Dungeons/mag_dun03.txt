//===== rAthena Script =======================================
//= Magma F3 Script
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================

// =============================
// ====  mob spawn
// =============================
mag_dun03,0,0	monster	Rigid Blazer	20372,30,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Nightmare Terror	20373,20,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Sky Deleter	20374,30,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Deleter	20375,20,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Explosion	20376,20,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Kaho	20377,20,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0	monster	Rigid Lava Golem	20378,10,5000,"#MagmaF3_Drop::OnMagma3MobDead"
mag_dun03,0,0,0,0	boss_monster	Rigid Muspellskoll	20419,1,7200000,600000,1,"#MagmaF3_Drop::OnMagma3MobDead"

// =============================
// ====  portal
// =============================
mag_dun02,245,68,0	warp	mag02-03	1,1,mag_dun03,121,120
mag_dun03,125,120,0	warp	mag03-02	1,1,mag_dun02,245,73

//==================================================
// Party Drop Script
//==================================================
mag_dun03,0,0,0	script	#MagmaF3_Drop	-1,{

OnMagma3MobDead:
	if(getcharid(1)) {
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		
		for ( .@p = 0; .@p < $@partymembercount; .@p++)
			if(attachrid($@partymemberaid[.@p],$@partymembercid[.@p]) == 1) {
				if(strcharinfo(3) != strnpcinfo(4)) continue;
				if(rand(1,10000) >= (10000 - .chance)) 
					getitem .drop_id[rand(getarraysize(.drop_id))],1;
			}

	} else {
		if(rand(1,10000) >= (10000 - .chance)) 
			getitem .drop_id[rand(getarraysize(.drop_id))],1;
	}
end;

OnInit:
	setarray .drop_id[0],
		15378, // Lava Leather Armor
		15379, // Lava Leather Suit
		15380, // Lava Leather Robe
		20935, // Lava Leather Manteau
		20936, // Lava Leather Muffler
		20937, // Lava Leather Hood
		22199, // Lava Leather Boots
		22200, // Lava Leather Shoes
		22201; // Lava Leather Sandals
	// ==== Drop Settings ====
	// คูณโอกาสดรอปด้านล่างด้วย อัตราดรอปของเซิร์ฟเวอร์สำหรับอุปกรณ์
	// 1 = เปิด
	// 0 = ปิด
	.battleconf_drop = 0;
	// โอกาสดรอปทั่วไป
	.drop = 50; // 50 = 0.5%
	// การคำนวณ
	.chance = .drop * ( (.battleconf_drop)?( (getbattleflag("item_rate_equip")%100 > 0)? (getbattleflag("item_rate_equip")/100):1):1);
	end;
}