//===== rAthena Script =======================================
//= saders enchant npc
//===== By: ==================================================
//= Sader1992
//https://rathena.org/board/profile/30766-sader1992/
//===== Current Version: =====================================
//= 3.1
//= Support random option.
//= fix error with removing ore thanks to @Patskie
//===== Compatible With: ===================================== 
//= rAthena Project
//https://rathena.org/board/files/file/3602-saders-enchantment-npc/
//https://github.com/sader1992/sader_scripts
//===== Update Log: =========================================
//===== Description: =========================================
// 3.1 FIX https://github.com/sader1992/sader_scripts/issues/4
//============================================================
//============================================================
mine_cave,88,30,5	script	Mining Enchanter	4_M_JOB_BLACKSMITH,{
	disable_items;
	mes .NPCname$;
	if(.s_only_vip){
		if(!vip_status(VIP_STATUS_ACTIVE)){
			mes "บริการนี้สำหรับ ผู้เล่น VIP เท่านั้น.";
			close;
		}
	}
	if (BaseLevel < .s_level_required[0]){
		mes "Base Level ของเจ้าต่ำเกินไป.";
		mes "   ";
		mes "Base Level ขั้นต่ำ คือ: ^FF0000"+.s_level_required[0]+"^000000.";
		close;
	}else if(BaseLevel > .s_level_required[1]){
		mes "Base Level ของเจ้าสูงเกินไป.";
		mes "   ";
		mes "Base Level สูงสุด คือ: ^FF0000"+.s_level_required[1]+"^000000.";
		close;
	}
	mes "สวัสดี !";
	mes "เจ้าต้องการที่จะออพชั่นอุปกรณ์หรือไม่?";
	mes "ข้าสามารถทำการออพชั่นให้กับเจ้าได้.";
	next;
	mes .NPCname$;
	if(.s_zeny > 0)
		mes "ซึ่งจะเสียค่าใช้จ่าย ^FF0000" + .s_zeny + "^000000 Zeny เท่านั้น!";
	if(.item_is_required)
		mes "และใช้ Enchantment Orb";
	mes "ข้าจะทำให้ดีที่สุดเพื่อให้การออพชั่นสำเร็จ!";
	mes "แต่จงจำไว้ว่า";
	mes "ต้องพึ่งดวงของเจ้าด้วย.";
	next;
	mes .NPCname$;
	mes "หากมีไอเท็มชนิดเดียวกันอยู่ในตัว";
	mes "ข้าจะไม่สามารถทำการออพชั่นให้กับเจ้าได้!";
	next;
	mes .NPCname$;
	.@string$[0] = ""+.Symbol$+" ออพชั่น";
	if(.remove_enchant)
		.@string$[1] = ""+.Symbol$+" ลบ ออพชั่น";
	if(.enable_the_shop)
		.@string$[2] = ""+.Symbol$+" ไอเทมที่เจ้าสามารถออพชั่นได้";
	mes "โปรดเลือก สิ่งที่เจ้าต้องการจะทำ!";
	menu .@string$[0],L_Enchant,.@string$[1],L_Remove,.@string$[2],-;
	callsub Q_shop; end;
	L_Remove: .@remove_orbs = true;
	L_Enchant: 
	next;
	mes .NPCname$;
	mes "โปรดเลือกไอเท็มที่เจ้าต้องการออพชั่น";
	for(.@i=0; .@i<getarraysize(.s_all$); .@i++)
		if(getequipid(.s_all_loc[.@i])>-1) {
			set .@armor_menu$, .@armor_menu$ + .s_all$[.@i] + " - [ ^E81B02" + getitemname(getequipid(.s_all_loc[.@i])) + "^000000 ]:";
		}else{
			set .@armor_menu$, .@armor_menu$ + .s_all$[.@i] + " - [ ^D6C4E8" + "ไม่ได้สวมใส่" + "^000000 ]:";
		}
	set .@s_all_selected, select(.@armor_menu$) -1;
	if(getequipid(.s_all_loc[.@s_all_selected])< 0){
		mes "เจ้าไม่มีไอเท็มติดตั้งที่นั่น";
		close;
	}
	if (countitem(getequipid(.s_all_loc[.@s_all_selected])) > 1){
			mes "เจ้ามีไอเท็มมากกว่าหนึ่งชิ้น";
			mes "จากรายการทีเจ้าต้องการที่จะออพชั่น";
			close;
	}
	.@s_item_refine = getequiprefinerycnt(.s_all_loc[.@s_all_selected]);
	if( getd(".specific_" + .s_all$[.@s_all_selected] + "s") ==1){
		for(.@i=0;.@i<getarraysize(getd("." + .s_all$[.@s_all_selected] + "s$"));.@i++){
			if(getequipid(.s_all_loc[.@s_all_selected]) == atoi(getd("." + .s_all$[.@s_all_selected] + "s$["+.@i+"]"))){
				.@good_to_go = true;
			}
		}
	}else{
		for(.@i=0;.@i<getarraysize(.black_list$);.@i++){
			if(getequipid(.s_all_loc[.@s_all_selected]) == atoi(.black_list$[.@i])){
				.@black_list_item = true;
			}
		}
		.@good_to_go = true;
	}
	if(!.@good_to_go || .@black_list_item){
		mes "ขอโทษ";
		mes " ข้าไม่สามารถออพชั่นไอเท็มนี้ได้.";
		close;
	}
	for(.@i=0;.@i<(MAX_ITEM_RDM_OPT);.@i++){
		.@r_id[.@i] = getequiprandomoption(.s_all_loc[.@s_all_selected],.@i,ROA_ID);
		.@r_v[.@i] = getequiprandomoption(.s_all_loc[.@s_all_selected],.@i,ROA_VALUE);
		.@r_p[.@i] = getequiprandomoption(.s_all_loc[.@s_all_selected],.@i,ROA_PARAM);
	}
	
	.@card0 = getequipcardid(.s_all_loc[.@s_all_selected],0);
	.@card1 = getequipcardid(.s_all_loc[.@s_all_selected],1);
	.@card2 = getequipcardid(.s_all_loc[.@s_all_selected],2);
	.@card3 = getequipcardid(.s_all_loc[.@s_all_selected],3);
	if(.@remove_orbs){
		next;
		mes .NPCname$;
		mes "การดำเนินการนี้จะนำการ์ดและออพชั่นทั้งหมดที่อยู่ในไอเท็มออก!";
		if (.s_zeny_remove > 0) {
			mes "เสียค่าใช้จ่าย ^FF0000" + .s_zeny_remove + "^000000 Zeny.";
		}
		mes "เจ้าแน่ใจไหม?";
			next;
			switch(select(""+.Symbol$+" ไม่ แน่ใจ",""+.Symbol$+" ใช่ แน่ใจ")){
				case 1: end;
				case 2:
					mes .NPCname$;
					mes "ข้าจะถามอีกครั้ง!";
					mes "เจ้าแน่ใจไหม?";
					next;
					switch(select(""+.Symbol$+" ไม่ แน่ใจ",""+.Symbol$+" ใช่ แน่ใจ")){
					case 1: end;
					case 2:
						mes .NPCname$; 
						if (Zeny < .s_zeny_remove) {
							mes "ขอโทษ, แต่เจ้ามี zeny ไม่เพียงพอ.";
							close;
						}
						mes "โปรดเลือกช่องที่เจ้าต้องการจะลบ";
						if(.select_remove_orb){
							if(.@card0 == 0).@card0$ = " - [ ^D6C4E8" + "ไม่ได้สวมใส่" + "^000000 ]"; else .@card0$ = getitemname(.@card0);
							if(.@card1 == 0).@card1$ = " - [ ^D6C4E8" + "ไม่ได้สวมใส่" + "^000000 ]"; else .@card1$ = getitemname(.@card1);
							if(.@card2 == 0).@card2$ = " - [ ^D6C4E8" + "ไม่ได้สวมใส่" + "^000000 ]"; else .@card2$ = getitemname(.@card2);
							if(.@card3 == 0).@card3$ = " - [ ^D6C4E8" + "ไม่ได้สวมใส่" + "^000000 ]"; else .@card3$ = getitemname(.@card3);
							switch(select(.@card0$,.@card1$,.@card2$,.@card3$)){
								case 1: .@card0 = 0; break;
								case 2: .@card1 = 0; break;
								case 3: .@card2 = 0; break;
								case 4: .@card3 = 0; break;
							}
							specialeffect2 EF_REPAIRWEAPON;
							set .@item, getequipid(.s_all_loc[.@s_all_selected]);
							delitem .@item,1;
							getitem3 .@item, 1, 1, .@s_item_refine, 0, .@card0, .@card1, .@card2, .@card3,.@r_id,.@r_v,.@r_p;
							Zeny -= .s_zeny_remove;
							end;
						}
						specialeffect2 EF_REPAIRWEAPON;
						set .@item, getequipid(.s_all_loc[.@s_all_selected]);
						delitem .@item,1;
						getitem3 .@item, 1, 1, .@s_item_refine, 0, 0, 0, 0, 0,.@r_id,.@r_v,.@r_p;
						Zeny -= .s_zeny_remove;
						end;
					}
			}
	}
	if(.chosse_orb){
		next;
		mes .NPCname$;
		mes "เลือก Orb ที่เจ้าต้องการ";
		for(.@i=0; .@i<getarraysize(getd("." + .s_all$[.@s_all_selected] + "$")); .@i++)
				set .@orb_menu$, .@orb_menu$ + getitemname(atoi(getd("." + .s_all$[.@s_all_selected] + "$["+.@i+"]"))) + ":";
		set .@s_orb_selected, select(.@orb_menu$) -1;
		.@selected_orb_id = getd("." + .s_all$[.@s_all_selected] + "$["+.@s_orb_selected+"]");
	}else{
		.@selected_orb_size = rand(getarraysize(getd("." + .s_all$[.@s_all_selected] + "$")));
		.@selected_orb_id = getd("." + .s_all$[.@s_all_selected] + "$["+.@selected_orb_size+"]");
	}
	next;
	mes .NPCname$;
	mes "สล็อตไหน?";
	for(.@i=getd(".slot_count_" + .s_all$[.@s_all_selected]); .@i<4; .@i++)
		if(getequipcardid(.s_all_loc[.@s_all_selected],.@i)!= null) {
			set .@slot_menu$, .@slot_menu$ + " [ ^E81B02" + getitemname(getequipcardid(.s_all_loc[.@s_all_selected],.@i)) + "^000000 ]:";
		}else{
			set .@slot_menu$, .@slot_menu$ + " [ ^D6C4E8" + "ว่างเปล่า" + "^000000 ]:";
		}
	set .@s_slot_selected, select(.@slot_menu$) -1;
	.@s_slot_selected += getd(".slot_count_" + .s_all$[.@s_all_selected]);
	if(!.s_enchant_overwrite){
		if(getequipcardid(.s_all_loc[.@s_all_selected],.@s_slot_selected) > 0){
			mes "เจ้ามี Orb อยู่แล้วในช่องนี้แล้ว";
			close;
		}
	}
	if (Zeny < .s_zeny) {
			mes "ขอโทษ, แต่เจ้ามี zeny ไม่เพียงพอ.";
			close;
		}
	if(.item_is_required && .chosse_orb){
		if (countitem(.@selected_orb_id) < 1){
			mes"เจ้าไม่มี Orb.";
			close;
		}
	}
	close2;
	specialeffect2 EF_MAPPILLAR;
	progressbar "ffff00",.progress_time;
	Zeny -= .s_zeny;
	mes .NPCname$;
	if(.item_is_required && .chosse_orb){delitem .@selected_orb_id,1;}
	if (rand(100) < .success_chanse[.@s_slot_selected]){
		mes "ยอดเยี่ยม";
		mes "ออพชั่น สำเร็จ!";
		specialeffect2 154;
		setd(".@card" + .@s_slot_selected, .@selected_orb_id);
		set .@item, getequipid(.s_all_loc[.@s_all_selected]);
		delitem .@item,1;
		getitem3 .@item, 1, 1, .@s_item_refine, 0, .@card0, .@card1, .@card2, .@card3,.@r_id,.@r_v,.@r_p;
		equip .@item;
		close;
	}else{
		specialeffect2 155;
		mes "ข้าขอโทษ";
		mes "ออพชั่น ไม่สำเร็จ!";
		specialeffect2 EF_PHARMACY_FAIL;
		if (rand(100) < .brack_chance){
			set .@item, getequipid(.s_all_loc[.@s_all_selected]);
			delitem .@item,1;
			mes "^FF0000และมันก็พัง!!^000000";
			specialeffect EF_SUI_EXPLOSION;
		}
		close;
	}
	
	
Q_shop:
	switch(select("Uppers:Germents:Shoses:Accessarys")){
	case 1: callshop "enchantable_items_Upper",1; break;
	case 2: callshop "enchantable_items_Germent",1; break;
	case 3: callshop "enchantable_items_Shose",1; break;
	case 4: callshop "enchantable_items_Accessary",1; break;
	}
end;	

OnInit:
	//--------------------------------------------------------------//
	//--------------------------------------------------------------//
	//--------------------   configuration   -----------------------//
	//--------------------------------------------------------------//
	//--------------------------------------------------------------//	
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	.Symbol$ = "~";
	//waitingroom " ออพชั่นอุปกรณ์ขุดแร่",0;
	//--------------------------------------------------------------//
	// เมนูสำหรับออพ
	//--------------------------------------------------------------//
	setarray .s_all$,"Upper","Germent","Shose","Accessary","Accessary";
	setarray .s_all_loc,EQI_HEAD_TOP,EQI_GARMENT,EQI_SHOES,EQI_ACC_L,EQI_ACC_R;
	
	//--------------------------------------------------------------//
	// Orbs IDs : รหัสลูกแก้วสำหรับออพ
	//--------------------------------------------------------------//
	setarray .Upper$,		910100,	//Mining Reduce Time 1
							910101,	//Mining Reduce Time 2
							910102;	//Mining Reduce Time 3
							
	setarray .Germent$,		910121,	//Rare Rate Up 1
							910122,	//Rare Rate Up 2
							910123,	//Rare Rate Up 3
							910124,	//Rare Rate Up 4
							910125,	//Rare Rate Up 5
							910126,	//Rare Rate Up 6
							910127,	//Rare Rate Up 7
							910128,	//Rare Rate Up 8
							910129,	//Rare Rate Up 9
							910130,	//Rare Rate Up 10
							910131,	//High Rate Up 1
							910132,	//High Rate Up 2
							910133,	//High Rate Up 3
							910134,	//High Rate Up 4
							910135,	//High Rate Up 5
							910136,	//High Rate Up 6
							910137,	//High Rate Up 7
							910138,	//High Rate Up 8
							910139,	//High Rate Up 9
							910140,	//High Rate Up 10
							910141,	//Medium Rate Up 1
							910142,	//Medium Rate Up 2
							910143,	//Medium Rate Up 3
							910144,	//Medium Rate Up 4
							910145,	//Medium Rate Up 5
							910146,	//Medium Rate Up 6
							910147,	//Medium Rate Up 7
							910148,	//Medium Rate Up 8
							910149,	//Medium Rate Up 9
							910150,	//Medium Rate Up 10
							910151,	//Normal Rate Up 1
							910152,	//Normal Rate Up 2
							910153,	//Normal Rate Up 3
							910154,	//Normal Rate Up 4
							910155,	//Normal Rate Up 5
							910156,	//Normal Rate Up 6
							910157,	//Normal Rate Up 7
							910158,	//Normal Rate Up 8
							910159,	//Normal Rate Up 9
							910160;	//Normal Rate Up 10
							
	setarray .Shose$,		910103,	//Mining Reduce Energy 1
							910104,	//Mining Reduce Energy 2
							910105;	//Mining Reduce Energy 3
							
	setarray .Accessary$,	910121,	//Rare Rate Up 1
							910122,	//Rare Rate Up 2
							910123,	//Rare Rate Up 3
							910124,	//Rare Rate Up 4
							910125,	//Rare Rate Up 5
							910126,	//Rare Rate Up 6
							910127,	//Rare Rate Up 7
							910128,	//Rare Rate Up 8
							910129,	//Rare Rate Up 9
							910130,	//Rare Rate Up 10
							910131,	//High Rate Up 1
							910132,	//High Rate Up 2
							910133,	//High Rate Up 3
							910134,	//High Rate Up 4
							910135,	//High Rate Up 5
							910136,	//High Rate Up 6
							910137,	//High Rate Up 7
							910138,	//High Rate Up 8
							910139,	//High Rate Up 9
							910140,	//High Rate Up 10
							910141,	//Medium Rate Up 1
							910142,	//Medium Rate Up 2
							910143,	//Medium Rate Up 3
							910144,	//Medium Rate Up 4
							910145,	//Medium Rate Up 5
							910146,	//Medium Rate Up 6
							910147,	//Medium Rate Up 7
							910148,	//Medium Rate Up 8
							910149,	//Medium Rate Up 9
							910150,	//Medium Rate Up 10
							910151,	//Normal Rate Up 1
							910152,	//Normal Rate Up 2
							910153,	//Normal Rate Up 3
							910154,	//Normal Rate Up 4
							910155,	//Normal Rate Up 5
							910156,	//Normal Rate Up 6
							910157,	//Normal Rate Up 7
							910158,	//Normal Rate Up 8
							910159,	//Normal Rate Up 9
							910160,	//Normal Rate Up 10
							910161,	//All Rate Up 1
							910162,	//All Rate Up 2
							910163,	//All Rate Up 3
							910164,	//All Rate Up 4
							910165,	//All Rate Up 5
							910166,	//All Rate Up 6
							910167,	//All Rate Up 7
							910168,	//All Rate Up 8
							910169,	//All Rate Up 9
							910170;	//All Rate Up 10
	
	//--------------------------------------------------------------//
	// เปิดใช้งานการออพทเฉพาะไอเท็มในรายการเท่านั้น
	//--------------------------------------------------------------//
	.specific_Uppers = true;
	.specific_Germents = true;
	.specific_Shoses = true;
	.specific_Accessarys = true;
	
	//--------------------------------------------------------------//
	// รายการไอเท็มที่สามารถออพได้
	//--------------------------------------------------------------//
	setarray .Uppers$,910082;
	setarray .Germents$,910084;
	setarray .Shoses$,910085;
	setarray .Accessarys$,910077,910078,910079,910080,910081,910083;
	
	//--------------------------------------------------------------//
	// Backlist Items
	//--------------------------------------------------------------//
	setarray .black_list$,2335,2338,2340,2341;
	
	//--------------------------------------------------------------//
	// กำหนดหมายเลขสล็อตได้
	//0 = ทั้ง 4 ช่อง ,1 = 3 ช่องสุดท้าย ,2 = 2 ช่องสุดท้าย ,3 = 1 ช่องสุดท้าย
	//--------------------------------------------------------------//
	.slot_count_Upper = 3;
	.slot_count_Germent = 3;
	.slot_count_Shose = 3;
	.slot_count_Accessary = 3;
	
	//--------------------------------------------------------------//
	// การกำหนดค่าอื่น ๆ
	//--------------------------------------------------------------//
	setarray .s_level_required,0,200;		//เลเวลที่จำเป็นในการใช้ npc
	.s_only_vip = false;					//ใช้ได้เฉพาะผู้เล่น vip true = เปิด ,false = ปิด
	setarray .success_chanse,20,40,60,100;	//โอกาสสำเร็จ [ช่องที่1,ช่องที่2,ช่องที่3,ช่องที่4]
	.s_zeny = 100000;						//ค่าใช้จ่าย (0 = ไม่ใช้)
	.s_zeny_remove = 100000;				//จำนวนเงินที่ใช้รีเซ็ต
	.item_is_required = false;				//เปิดใช้ลูกแก้วในการออพ true = เปิด , false = ปิด(ถ้า .chosse_orb = false ฟังชั่นนี้ก็จะเป็น false เหมือนกัน)
	.s_enchant_overwrite = false;			//เปิดใช้งานการออพทับช่องเดิม true = เปิด , false = ปิด
	.progress_time = 7;						//เวลาที่ต้องรอจนกว่าจะสิ้นสุด
	.chosse_orb = false;					//เลือกลูกแก้ว false = สุ่ม ,true = เลือก
	.brack_chance = 10;						//โอกาสที่มันจะแตกหากล้มเหลว
	.remove_enchant = true;					//ลบออพ true = เปิด , false = ปิด
	.select_remove_orb = true;				//เลือกออพที่ต้องการลบ true = เปิด , false = ปิด
	//--------------------------------------------------------------//
	// สิ่งนี้จะแสดงเฉพาะรายการที่ npc สามารถออพได้ในร้านค้า แต่ไม่สามารถซื้อได้
	//--------------------------------------------------------------//
	.enable_the_shop = true;

	//--------------------------------------------------------------//
	// ห้ามแก้ไขที่นี่
	//--------------------------------------------------------------//
	npcshopdelitem "enchantable_items_Upper",512;
	npcshopdelitem "enchantable_items_Germent",512;
	npcshopdelitem "enchantable_items_Shose",512;
	npcshopdelitem "enchantable_items_Accessary",512;
	
	for (.@i = 0; .@i < getarraysize(.Uppers$); .@i++)
		npcshopadditem "enchantable_items_Upper", atoi(.Uppers$[.@i]),1;
	for (.@i = 0; .@i < getarraysize(.Germents$); .@i++)
		npcshopadditem "enchantable_items_Germent", atoi(.Germents$[.@i]),1;	
	for (.@i = 0; .@i < getarraysize(.Shoses$); .@i++)
		npcshopadditem "enchantable_items_Shose", atoi(.Shoses$[.@i]),1;
	for (.@i = 0; .@i < getarraysize(.Accessarys$); .@i++)
		npcshopadditem "enchantable_items_Accessary", atoi(.Accessarys$[.@i]),1;
	end;
}
-	pointshop	enchantable_items_Upper	-1,#YOU_CAN_ENCHANT_Uppers,512:1;
-	pointshop	enchantable_items_Germent	-1,#YOU_CAN_ENCHANT_Germents,512:1;
-	pointshop	enchantable_items_Shose	-1,#YOU_CAN_ENCHANT_Shoses,512:1;
-	pointshop	enchantable_items_Accessary	-1,#YOU_CAN_ENCHANT_Accessarys,512:1;



