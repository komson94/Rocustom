//===== rAthena Script =======================================
//= Hat Maker Shop
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================
// Dummy Shop
-	shop	HAT_MAKER_SHOP3	-1,501:-1

fct_in01,99,141,3	script	Hat Maker#3	4_M_NFMAN,{
	disable_items;
	mes .NPCname$;
	mes "ยินดีต้อนรับเหล่านักผจญภัย";
	mes "ข้าคือช่างทำหมวก";
	mes "ข้ามีหมวกหลากหลายแบบให้เจ้าเลือก";
	mes "เจ้าสนใจชิ้นไหนล่ะ?";
	callshop "HAT_MAKER_SHOP3",1;
	npcshopattach "HAT_MAKER_SHOP3";
	end;

OnBuyItem:
	if(getarraysize(@bought_nameid) > 1){
		mes .NPCname$;
		mes "ไม่สามารถซื้อสินค้า 2 รายการหรือมากกว่าในเวลาเดียวกัน.";
		close;
	}

	set .@shop_type$, "HAT_MAKER_SHOP3";

	for(set .@l,0; .@l < getarraysize(getd("."+.@shop_type$+"$")); .@l++){

		explode(.@x$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

		if(@bought_nameid == atoi(.@x$[0])){
			explode(.@s$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

			for(set .@k,0; .@k < getarraysize(.@s$); set .@k,.@k + 1){
				set .@its[.@k], atoi(.@s$[.@k]);
			}

			mes .NPCname$;
			mes "[ รายการที่เลือก ] :";
			mes "ไอเท็ม: ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+"^000000";
			mes "จำนวน: ^0000FF"+@bought_quantity+"^000000";
			mes " ";
			mes "[ รายการที่ต้องการ ] :";
			for(set .@h,2; .@h < getarraysize(.@its); set .@h,.@h + 2){
				if(countitem(.@its[.@h]) >= .@its[.@h+1] * @bought_quantity){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ff"+getitemname(.@its[.@h])+""+((getitemslots(.@its[.@h]) > 0)?"[" + getitemslots(.@its[.@h]) + "]":"")+"^000000 ("+getd(".@color$")+""+countitem(.@its[.@h])+"/"+(.@its[.@h+1]*@bought_quantity)+"^000000)";
			}
			if(.@its[1] > 0){
				if(Zeny >= (.@its[1] * @bought_quantity)){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ffZeny^000000 ("+getd(".@color$")+""+Zeny+"/"+(.@its[1] * @bought_quantity)+"^000000)";
			}
			next;
			if(select("> ซื้อ ^0000ff"+getitemname(@bought_nameid)+"^000000 x "+@bought_quantity+"") == 1){
				if((.@its[1] * @bought_quantity) > Zeny){
						mes .NPCname$;
						mes "เจ้ามี Zeny ไม่เพียงพอ.";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
				}
				for(set .@j,2; .@j < getarraysize(.@its); set .@j,.@j + 2){
					if(countitem(.@its[.@j]) < (.@its[.@j+1]) * @bought_quantity){
						mes .NPCname$;
						mes "รายการที่ต้องการไม่เพียงพอ..";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
					}
				}
				for(set .@m,2; .@m < getarraysize(.@its); set .@m,.@m + 2){
					delitem .@its[.@m],(.@its[.@m+1] * @bought_quantity);
				}
				if(.@its[1] > 0){
					Zeny -= (.@its[1] * @bought_quantity);
				}
				specialeffect2 305;
				mes .NPCname$;
				mes "ยินดีด้วย! เจ้าได้รับ ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+" x "+@bought_quantity+"^000000.";

				getitem .@its[0], @bought_quantity;
				deletearray @bought_quantity, getarraysize(@bought_quantity);
				deletearray @bought_nameid, getarraysize(@bought_nameid);
				close;
			}
			break;
		}

	}


OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	//- Remove the dummy item.
	npcshopdelitem "HAT_MAKER_SHOP3",501,50;
	//- <ItemID>|<Zeny>|<ItemReq>|<ItemReqAmount>|<ItemIDReq1>|,....
	setarray .HAT_MAKER_SHOP3$[0],
			"2272|91100|1019|50|983|1",//Stop Post
			"5062|0|2280|1|7197|300|7150|300",//Straw Hat
			"5049|0|1099|1500",//Striped Hairband
			"5032|0|2221|1|2227|1|1059|250|7063|600",//Sunday Hat
			"5059|0|5030|1|7161|300|7217|100|7213|100",//Teddybear Hat
			"5077|0|2278|1|975|1",//Tulip Hairpin
			"5171|10000000|4219|1|4114|1|4177|1|4259|1|4212|1|4073|1|4112|1|4081|1|4251|1|4166|1|7511|1000|7563|1000",//Valkyrie Helm
			"2292|2000|999|50",//Welding Mask
			"5115|50000|7267|999|983|1|749|1",//Winter Hat
			"5050|0|5037|1|7064|500",//Wonder Nutshell
			"5079|0|2294|1|7220|400",//X Hairpin
			"5121|0|7315|369|7263|1|660|1|7099|30",//Zealotus Mask
			"5374|990000|6004|1|2256|1|7799|30|7798|50";//Gigantic Magestic Goat
		

	if(!.itemload){
		//- Add item in the shop.
		for(set .@p,0; .@p < getarraysize(getvariableofnpc(.HAT_MAKER_SHOP3$,"Hat Maker#3")); .@p++){
			explode(.@x$,getvariableofnpc(.HAT_MAKER_SHOP3$[.@p],"Hat Maker#3"),"|");

			for(set .@k,0; .@k < getarraysize(.@x$); .@k++){
				set .@it[.@k], atoi(.@x$[.@k]);
			}
			npcshopadditem "HAT_MAKER_SHOP3",.@it[0], 0;
		}
	}
	set .itemload, 1;
end;

OnTouch:
	deletearray @bought_quantity, getarraysize(@bought_quantity);
	deletearray @bought_nameid, getarraysize(@bought_nameid);
end;
}