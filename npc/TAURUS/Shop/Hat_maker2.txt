//===== rAthena Script =======================================
//= Hat Maker Shop
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0 Add Shop NPC with Barter Shop Src.
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================
// Dummy Shop
-	shop	HAT_MAKER_SHOP2	-1,501:-1

fct_in01,99,143,3	script	Hat Maker#2	4_M_NFMAN,{
	disable_items;
	mes .NPCname$;
	mes "ยินดีต้อนรับเหล่านักผจญภัย";
	mes "ข้าคือช่างทำหมวก";
	mes "ข้ามีหมวกหลากหลายแบบให้เจ้าเลือก";
	mes "เจ้าสนใจชิ้นไหนล่ะ?";
	callshop "HAT_MAKER_SHOP2",1;
	npcshopattach "HAT_MAKER_SHOP2";
	end;

OnBuyItem:
	if(getarraysize(@bought_nameid) > 1){
		mes .NPCname$;
		mes "ไม่สามารถซื้อสินค้า 2 รายการหรือมากกว่าในเวลาเดียวกัน.";
		close;
	}

	set .@shop_type$, "HAT_MAKER_SHOP2";

	for(set .@l,0; .@l < getarraysize(getd("."+.@shop_type$+"$")); .@l++){

		explode(.@x$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

		if(@bought_nameid == atoi(.@x$[0])){
			explode(.@s$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

			for(set .@k,0; .@k < getarraysize(.@s$); set .@k,.@k + 1){
				set .@its[.@k], atoi(.@s$[.@k]);
			}

			mes .NPCname$;
			mes "[ รายการที่เลือก ] :";
			mes "ไอเท็ม: ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+"^000000";
			mes "จำนวน: ^0000FF"+@bought_quantity+"^000000";
			mes " ";
			mes "[ รายการที่ต้องการ ] :";
			for(set .@h,2; .@h < getarraysize(.@its); set .@h,.@h + 2){
				if(countitem(.@its[.@h]) >= .@its[.@h+1] * @bought_quantity){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ff"+getitemname(.@its[.@h])+""+((getitemslots(.@its[.@h]) > 0)?"[" + getitemslots(.@its[.@h]) + "]":"")+"^000000 ("+getd(".@color$")+""+countitem(.@its[.@h])+"/"+(.@its[.@h+1]*@bought_quantity)+"^000000)";
			}
			if(.@its[1] > 0){
				if(Zeny >= (.@its[1] * @bought_quantity)){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ffZeny^000000 ("+getd(".@color$")+""+Zeny+"/"+(.@its[1] * @bought_quantity)+"^000000)";
			}
			next;
			if(select("> ซื้อ ^0000ff"+getitemname(@bought_nameid)+"^000000 x "+@bought_quantity+"") == 1){
				if((.@its[1] * @bought_quantity) > Zeny){
						mes .NPCname$;
						mes "เจ้ามี Zeny ไม่เพียงพอ.";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
				}
				for(set .@j,2; .@j < getarraysize(.@its); set .@j,.@j + 2){
					if(countitem(.@its[.@j]) < (.@its[.@j+1]) * @bought_quantity){
						mes .NPCname$;
						mes "รายการที่ต้องการไม่เพียงพอ..";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
					}
				}
				for(set .@m,2; .@m < getarraysize(.@its); set .@m,.@m + 2){
					delitem .@its[.@m],(.@its[.@m+1] * @bought_quantity);
				}
				if(.@its[1] > 0){
					Zeny -= (.@its[1] * @bought_quantity);
				}
				specialeffect2 305;
				mes .NPCname$;
				mes "ยินดีด้วย! เจ้าได้รับ ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+" x "+@bought_quantity+"^000000.";

				getitem .@its[0], @bought_quantity;
				deletearray @bought_quantity, getarraysize(@bought_quantity);
				deletearray @bought_nameid, getarraysize(@bought_nameid);
				close;
			}
			break;
		}

	}


OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	//- Remove the dummy item.
	npcshopdelitem "HAT_MAKER_SHOP2",501,50;
	//- <ItemID>|<Zeny>|<ItemReq>|<ItemReqAmount>|<ItemIDReq1>|,....
	setarray .HAT_MAKER_SHOP2$[0],
			"5061|20000|2269|1|999|10",//Flower Hairpin
			"5063|0|970|1|930|500",//Giant Band Aid
			"5021|0|2233|1|969|1|999|20|949|80|938|800",//Grief for Greed
			"5176|0|706|1|7201|500|7200|10|7015|20|952|100|1028|100",//Hahoe Mask
			"5022|0|7086|1|969|10|999|40|1003|50|984|2",//Hat of the Sun God
			"5001|0|999|40|984|1|970|1|1003|1",//Headset
			"5041|0|7013|1200",//Heart Hairpin
			"5025|0|2229|1|2254|1|7036|5",//Helm of Angel
			"5094|0|909|10000|931|10000|968|100",//Helmet of Orc Hero
			"5070|0|7216|300|7097|300|2211|1|982|1",//Hot-blooded Headband
			"5071|0|5010|1|5049|1|7101|10",//Indian Headband
			"5069|0|1022|999",//Kitsune Mask
			"5084|0|1026|1000|7065|100|945|10|7030|1",//Lazy Smokie
			"5027|0|2252|1|1036|400|7001|50|4052|1",//Mage Hat
			"5045|0|2252|1|1054|450|943|1200",//Magician Hat
			"5173|0|7445|7|7446|7|7447|7|7448|7|954|300|983|1|733|5|2221|1",//Magistrate Hat
			"5031|0|5009|1|5028|1|747|1|999|25",//Mine Hat
			"5073|0|2285|1|1550|1",//Model Training Hat
			"2278|0|909|10|914|10|705|10",//Mr.Smile
			"5117|0|731|10|748|3|982|1",//Mystic Rose
			"5177|0|706|1|7201|500|7200|10|1048|500|1053|2|980|1",//Mythical Lion Mask
			"2281|5000|707|1|999|20",//Opera Masque
			"5043|0|2281|1|1048|50",//Opera Phantom Mask
			"5004|0|701|5",//Oxygen Mask
			"5023|0|1059|150|907|100|978|1",//Parcel Hat
			"5060|0|2236|1|7151|100|7111|100",//Party Hat
			"5012|0|710|1|703|1|704|1|708|1",//Ph.D Hat
			"2293|0|1049|4",//Pretend Murdered
			"5033|0|2213|1|1036|20|7065|300|7012|200",//Raccoon Hat
			"5039|0|5015|1|978|1|7030|50",//Rainbow Eggshell
			"5109|0|10015|1|975|1|10007|1|5032|1",//Red Bonnet
			"5083|0|2209|1|2244|1|10007|1",//Red Ribbon
			"5108|0|611|10|7301|1887|5120|1",//Renown Detective's Cap
			"2280|10000|1019|120",//Sakkat
			"5078|0|5033|1|5064|1",//Sea-Otter Hat
			"5243|0|1022|20|7038|10|7166|10|7065|20|7217|1",//Chullos
			"5064|0|945|600|7030|1",//Smokie Leaf
			"5067|0|5062|1|952|50|1907|1",//Sombrero
			"5029|0|1015|1|7068|300|7033|850";//Spore Hat
			

	if(!.itemload){
		//- Add item in the shop.
		for(set .@p,0; .@p < getarraysize(getvariableofnpc(.HAT_MAKER_SHOP2$,"Hat Maker#2")); .@p++){
			explode(.@x$,getvariableofnpc(.HAT_MAKER_SHOP2$[.@p],"Hat Maker#2"),"|");

			for(set .@k,0; .@k < getarraysize(.@x$); .@k++){
				set .@it[.@k], atoi(.@x$[.@k]);
			}
			npcshopadditem "HAT_MAKER_SHOP2",.@it[0], 0;
		}
	}
	set .itemload, 1;
end;

OnTouch:
	deletearray @bought_quantity, getarraysize(@bought_quantity);
	deletearray @bought_nameid, getarraysize(@bought_nameid);
end;
}