//===== rAthena Script =======================================
//= Hat Maker Shop
//===== By: ================================================== 
//= mr.TAURUS
//===== Current Version: =====================================
//= 1.0 Add Shop NPC with Barter Shop Src.
//===== Compatible With: ===================================== 
//= rAthena Project
//============================================================
// Dummy Shop
-	shop	HAT_MAKER_SHOP	-1,501:-1

fct_in01,99,145,3	script	Hat Maker#1	4_M_NFMAN,{
	disable_items;
	mes .NPCname$;
	mes "ยินดีต้อนรับเหล่านักผจญภัย";
	mes "ข้าคือช่างทำหมวก";
	mes "ข้ามีหมวกหลากหลายแบบให้เจ้าเลือก";
	mes "เจ้าสนใจชิ้นไหนล่ะ?";
	callshop "HAT_MAKER_SHOP",1;
	npcshopattach "HAT_MAKER_SHOP";
	end;

OnBuyItem:
	if(getarraysize(@bought_nameid) > 1){
		mes .NPCname$;
		mes "ไม่สามารถซื้อสินค้า 2 รายการหรือมากกว่าในเวลาเดียวกัน.";
		close;
	}

	set .@shop_type$, "HAT_MAKER_SHOP";

	for(set .@l,0; .@l < getarraysize(getd("."+.@shop_type$+"$")); .@l++){

		explode(.@x$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

		if(@bought_nameid == atoi(.@x$[0])){
			explode(.@s$,getd("."+.@shop_type$+"$["+.@l+"]"),"|");

			for(set .@k,0; .@k < getarraysize(.@s$); set .@k,.@k + 1){
				set .@its[.@k], atoi(.@s$[.@k]);
			}

			mes .NPCname$;
			mes "[ รายการที่เลือก ] :";
			mes "ไอเท็ม: ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+"^000000";
			mes "จำนวน: ^0000FF"+@bought_quantity+"^000000";
			mes " ";
			mes "[ รายการที่ต้องการ ] :";
			for(set .@h,2; .@h < getarraysize(.@its); set .@h,.@h + 2){
				if(countitem(.@its[.@h]) >= .@its[.@h+1] * @bought_quantity){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ff"+getitemname(.@its[.@h])+""+((getitemslots(.@its[.@h]) > 0)?"[" + getitemslots(.@its[.@h]) + "]":"")+"^000000 ("+getd(".@color$")+""+countitem(.@its[.@h])+"/"+(.@its[.@h+1]*@bought_quantity)+"^000000)";
			}
			if(.@its[1] > 0){
				if(Zeny >= (.@its[1] * @bought_quantity)){
					.@color$ = "^027002";
				}else{
					.@color$ = "^ff0000";
				}
				mes "> ^0000ffZeny^000000 ("+getd(".@color$")+""+Zeny+"/"+(.@its[1] * @bought_quantity)+"^000000)";
			}
			next;
			if(select("> ซื้อ ^0000ff"+getitemname(@bought_nameid)+"^000000 x "+@bought_quantity+"") == 1){
				if((.@its[1] * @bought_quantity) > Zeny){
						mes .NPCname$;
						mes "เจ้ามี Zeny ไม่เพียงพอ.";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
				}
				for(set .@j,2; .@j < getarraysize(.@its); set .@j,.@j + 2){
					if(countitem(.@its[.@j]) < (.@its[.@j+1]) * @bought_quantity){
						mes .NPCname$;
						mes "รายการที่ต้องการไม่เพียงพอ..";
						deletearray @bought_quantity, getarraysize(@bought_quantity);
						deletearray @bought_nameid, getarraysize(@bought_nameid);
						close;
					}
				}
				for(set .@m,2; .@m < getarraysize(.@its); set .@m,.@m + 2){
					delitem .@its[.@m],(.@its[.@m+1] * @bought_quantity);
				}
				if(.@its[1] > 0){
					Zeny -= (.@its[1] * @bought_quantity);
				}
				specialeffect2 305;
				mes .NPCname$;
				mes "ยินดีด้วย! เจ้าได้รับ ^0000FF"+getitemname(@bought_nameid)+""+((getitemslots(@bought_nameid) > 0)?"[" + getitemslots(@bought_nameid) + "]":"")+" x "+@bought_quantity+"^000000.";

				getitem .@its[0], @bought_quantity;
				deletearray @bought_quantity, getarraysize(@bought_quantity);
				deletearray @bought_nameid, getarraysize(@bought_nameid);
				close;
			}
			break;
		}

	}


OnInit:
	.NPCname$ = "[^0000FF "+strnpcinfo(1)+" ^000000]";
	//- Remove the dummy item.
	npcshopdelitem "HAT_MAKER_SHOP",501,50;
	//- <ItemID>|<Zeny>|<ItemReq>|<ItemReqAmount>|<ItemIDReq1>|,....
	setarray .HAT_MAKER_SHOP$[0],		
			"5086|0|1095|3000|2288|1",//Alarm Mask
			"5074|20000|2254|1|2286|1",//Angel Wing Ears
			"2284|0|923|20",//Antlers
			"5174|0|7445|7|7446|7|7447|7|7448|7|954|300|975|1|733|5|7166|50",//Ayum
			"5110|0|526|1|7270|1|10004|1|941|1",//Baby Pacifier
			"5042|0|968|50|10007|1",//Bao Bao
			"5076|0|2226|1|7038|500",//Beanie
			"2296|50000|2243|1|999|100",//Binoculars
			"5057|10000|2213|1|983|1|914|200",//Black Cat Ears
			"5065|0|624|1|959|300|544|50|1023|1|7030|1|938|100",//Blue Fish
			"5052|0|2211|1|978|1|7003|300",//Blue Hairband
			"5040|0|7047|100",//Blush
			"5016|0|1030|10",//Boy's Cap
			"5436|0|1032|1000|10007|1",//Bride's Corolla
			"5169|0|706|1|7201|500|7200|10|1049|20|1059|500|1054|2|1024|100",//Bride Mask
			"5034|0|2233|1|746|20",//Bulb Band
			"2214|0|2213|1|722|1|706|1|949|100",//Bunny Band
			"5024|0|529|10|530|5|539|20|538|15|999|10",//Cake Hat
			"5028|0|2279|1|7035|50|526|100",//Candle
			"5175|0|983|1|7111|100|938|99",//Censor Bar
			"5026|0|1036|450|949|330|539|120|982|1",//Chef Hat
			"5075|0|2248|1|7030|108|7194|108|7120|4",//Cowboy Hat
			"5048|0|5041|1|999|10",//Cresent Hairpin
			"5036|0|2608|1|7069|500",//Cross Hat
			"5080|20000|10006|1|714|1|969|3",//Crown of Ancient Queen
			"5081|40000|2249|1|714|1|969|3",//Crown of Mistress
			"5107|0|519|50|7031|50|548|50|539|50",//Crunch Toast
			"5091|20000|10016|1|714|1|969|3",//Decorative Golden Bell
			"5082|0|921|300",//Decorative Mushroom
			"5038|0|1038|600|7048|40",//Deviruchi Hat
			"2273|3500|2275|1|998|50|733|1",//Doctor Band
			"5058|0|2233|1|983|1|7030|1|7206|300",//Drooping Kitty
			"2283|5000|5001|1|724|1|949|200",//Ear Muffs
			"5068|20000|2255|1|2286|1",//Evil Wing Ears
			"5047|0|2271|1|975|1",//Fashionable Glasses
			"5170|0|5172|1|7063|100|982|1",//Feather Beret
			"5018|500|2247|1|916|300";//Feather Bonnet
			

	if(!.itemload){
		//- Add item in the shop.
		for(set .@p,0; .@p < getarraysize(getvariableofnpc(.HAT_MAKER_SHOP$,"Hat Maker#1")); .@p++){
			explode(.@x$,getvariableofnpc(.HAT_MAKER_SHOP$[.@p],"Hat Maker#1"),"|");

			for(set .@k,0; .@k < getarraysize(.@x$); .@k++){
				set .@it[.@k], atoi(.@x$[.@k]);
			}
			npcshopadditem "HAT_MAKER_SHOP",.@it[0], 0;
		}
	}
	set .itemload, 1;
end;

OnTouch:
	deletearray @bought_quantity, getarraysize(@bought_quantity);
	deletearray @bought_nameid, getarraysize(@bought_nameid);
end;
}