//===== Atlantis Script ======================================
//= iRO Bounty Boards
//===== By: ==================================================
//= Ragno / AtlantisRO
//===== Actual Version: ======================================
//= 1.1 Added vip function to collect all bounties,
//=     Fixed a minnor bug with one use of a close command.
//= 1.0.2 Removed += operations with strings.
//= 1.0.1 Changed npc and monsters constants to numerical ids
//= 1.0 Starting Release
//===== General Description: =================================
//= Hunting Quest designed by iRO:
//=    http://irowiki.org/wiki/Bounty_Board_Quests
//===== How to Use: ==========================================
//= Uses function "F_Bounty_Board" to manage the Bounty Boards
//= npc on cities.
//= The function arguments are this:
//= 	callfunc "F_Bounty_Board", MinLevel, MaxLevel, MsgHuntPlace, MsgOutLevel,
//=			MonsterID, 		QuestID, 	BaseExp, 	JobExp{, 
//=			MonsterID, 		QuestID, 	BaseExp, 	JobExp};
//============================================================

function	script	F_Bounty_Board	{
	if (BaseLevel < getarg(0) || (BaseLevel > getarg(1) && getarg(1))){
		for (.@i = 5; .@i < getargcount(); .@i += 4){
			if (checkquest(getarg(.@i)) != -1){
				mes "[Bounty Board]";
				mes "คุณพร้อมที่จะรับรางวัลของคุณแล้ว.";
				mes "คุณต้องการรับรางวัลอันไหน?";
				next;
				switch(select("รับ 1 รางวัล", "รวบรวมรางวัลทั้งหมด", "ฉันต้องการออกจากการล่า", "ยกเลิก")){
					case 1: goto L_BountyOne;
					case 2: goto L_BountyAll;
					case 3: goto L_BountyDel;
					case 4: close;
				}
			}
		}
		switch(getarg(3)){
			case 1: mes "ภารกิจเหล่านี้ใช้ได้เฉพาะกับนักผจญภัยที่อยู่ระหว่างระดับ "+getarg(0)+"-"+getarg(1)+"."; break;
			case 2: mes "ภารกิจเหล่านี้ใช้ได้เฉพาะกับนักผจญภัยที่อยู่ระหว่างระดับ "+getarg(0)+" และ "+getarg(1)+"."; break;
			case 3: mes "ภารกิจเหล่านี้ใช้ได้เฉพาะกับนักผจญภัยที่อยู่เหนือระดับ "+getarg(0)+"."; break;
			case 4: mes "ภารกิจเหล่านี้ใช้ได้เฉพาะกับนักผจญภัยที่สูงกว่าระดับ "+getarg(0)+"."; break;
			case 5: mes "คุณสะดุดกับสิ่งที่ดูเหมือนจะเป็นกระดานข่าวที่มีโฆษณาที่ต้องการไม่มีสิ่งใดที่น่าสนใจ."; break;
		}
		close;
	}
	mes "[Bounty Board]";
	mes "คุณพบสิ่งที่ดูเหมือนจะเป็นกระดานข่าวที่มีโฆษณาที่ต้องการ.";
	mes "ลองดูให้ละเอียด?";
	next;
	mes "[Bounty Board]";
	mes "มีรายการมากมายสำหรับการล่าสัตว์ประหลาด.";
	next;
	switch(select("ตรวจสอบภารกิจ.", "รับรางวัล.", "ฉันจะไปตามล่าที่ไหน?", "ค้นหานักล่าเงินรางวัลอื่น ๆ.", "ฉันต้องการออกจากการล่า.", "ไม่สนใจ.")){
	case 1:	// Check bounty missions
		mes "[Bounty Board]";
		mes "มีรายชื่อสัตว์ประหลาดอยู่บนกระดาน.";
		mes "เมื่อพิจารณาถึงเรื่องนี้คุณตัดสินใจที่จะทำภารกิจล่าสัตว์.";
		next;
		for (.@i = 4; .@i < getargcount(); .@i += 4)
			.@menu$ = .@menu$ + getmonsterinfo(getarg(.@i),MOB_NAME) + ":";
		.@menu$ = .@menu$ + "ล่าทั้งหมด:ยกเลิก";
		.@bounty = (select(.@menu$) * 4) + 1;
		if (.@bounty > getargcount()+1){
			mes "[Bounty Board]";
			mes "คุณตัดสินใจที่จะไม่รับรางวัลใด ๆ.";
			close;
		} else if (.@bounty == getargcount()+1){
			for (.@i = 5; .@i < getargcount(); .@i += 4){
				if (checkquest(getarg(.@i)) == -1)
					setquest getarg(.@i);
				.@HuntAllList$ = .@HuntAllList$ + (.@i+4 > getargcount()?" และ ":(.@i > 5?", ":"")) + getmonsterinfo(getarg(.@i-1),MOB_NAME);
			}
			mes "[Bounty Board]";
			mes "มีค่าหัวให้ล่า  " + .@HuntAllList$ + " ทั้งหมด 150 ตัว.";
			close;
		}
		.@monster$ = getmonsterinfo(getarg(.@bounty-1),MOB_NAME);
		mes "[Bounty Board]";
		mes "มีค่าหัวให้ล่า " + .@monster$ + " ทั้งหมด 150 ตัว.";
		next;
		if (select("ฉันจะทำมัน.", "เอ่อ ไม่เป็นไร.") == 1){
			if (checkquest(getarg(.@bounty),HUNTING) == -1)
				setquest getarg(.@bounty);
			mes "[Bounty Board]";
			mes "คุณได้รับรางวัลการล่า "+.@monster$+".";
		}
		close;
	case 2:	// Collect bounty rewards
		mes "[Bounty Board]";
		mes "คุณพร้อมที่จะรับรางวัลของคุณแล้ว";
		mes "คุณต้องการรับรางวัลอันไหน?";
		next;
		switch(select("รับ 1 รางวัล", "รวบรวมรางวัลทั้งหมด", "ยกเลิก")){
			case 1: goto L_BountyOne;
			case 2: goto L_BountyAll;
			case 3: close;
		}
	case 3:	// Where do I go to hunt?
		mes "[Bounty Board]";
		switch(getarg(2)){
			case 1: mes "คุณสามารถตามล่าพวกมันได้ในคุกใต้ดินคุณกำลังจ้องมองที่ทางเข้าเดี๋ยวนี้!"; break;
			case 2: mes "คุณสามารถตามล่าพวกมันในป่ารอบ ๆ Payon และที่ถ้ำขึ้นไปทางเหนือที่ Archer Village."; break;
			case 3: mes "คุณสามารถตามล่าพวกมันได้ในหลาย ๆ พื้นที่รอบ ๆ Prontera."; break;
			case 4: mes "คุณสามารถตามล่าพวกมันได้ในทุ่งและดันเจี้ยนรอบ ๆ Prontera.";  break;
			case 5: mes "คุณสามารถตามล่า Umbala Fields เพื่อเติมเต็มค่าหัวเหล่านี้."; break;
			case 6:
				mes "คุณต้องการไปล่าสัตว์ประหลาดในพื้นที่ใด?";
				next;
				.@bounty = (select("Golem", "Stalactic Golem", "Medusa", "ยกเลิก") * 4) + 1;
				if (.@bounty > getargcount())
					close;
				mes "[Bounty Board]";
				if (checkquest(getarg(.@bounty),HUNTING) == -1){
					mes "คุณไม่ได้ล่า "+getmonsterinfo(getarg(.@bounty-1),MOB_NAME)+".";
					close;
				}
				mes "ส่งคุณตอนนี้.";
				close2;
				switch(.@bounty){
					case 1: warp "cmd_fild06",147,36; break;
					case 2: warp "beach_dun2",154,25; break;
					case 3: warp "beach_dun",266,67; break;
				}
				end;
			case 7: mes "คุณสามารถตามล่าได้ที่ <NAVI> Juno Fields <INFO> yuno, 158,16, </INFO> </NAVI> เพื่อเติมเต็มค่าเหล่านี้"; break;
			case 8: mes "คุณสามารถตามล่าได้ที่ <NAVI>Brasilis Field<INFO>brasilis,309,304,</INFO></NAVI> and <NAVI>Brasilis Dungeon<INFO>brasilis,183,284,</INFO></NAVI> เพื่อเติมเต็มค่าเหล่านี้."; break;
			case 9: mes "คุณสามารถตามล่าได้ที่ various fields surrounding Lighthalzen."; break;
			case 10: mes "คุณสามารถตามล่าได้ที่ various fields และ dungeons รอบๆ Lighthalzen."; break;
			case 11:
				mes "คุณสามารถตามล่าได้ที่ <NAVI>Splendide Field<INFO>mid_camp,14,143,</INFO></NAVI> เพื่อเติมเต็มค่าเหล่านี้.";
				mes "ต้องแน่ใจว่าคุณเป็นสมาชิกที่ได้รับอนุญาตของ Midgard Alliance ก่อนไปที่นั่น.";
				break;
			case 12: mes "คุณสามารถตามล่าได้ที่ Dicastes Fields เพื่อเติมเต็มค่าเหล่านี้."; break;
		}
		close;
	case 4:	// Search for other bounty hunters.
		mes "[Bounty Board]";
		mes "ในการค้นหาผู้เล่นปาร์ตี้คลิกที่ปุ่ม [การจอง] ในเมนูผู้ใช้หรือประเภทของคุณ \"/booking\" ในกล่องแชทเพื่อเปิดหน้าต่าง.";
		next;
		mes "[Bounty Board]";
		mes "จากนั้นคลิก \"Search\" เพื่อแสดงรายการปาร์ตี้ทั้งหมด.";
		mes "ในการโพสต์โฆษณาของบุคคลที่ต้องการ, type \"/recruit\" ลงในกล่องแชท.";
		close;
	case 5:	// I want to QUIT hunting
		goto L_BountyDel;
	case 6:	// Not interested.
		mes "[Bounty Board]";
		mes "ชีวิตเต็มไปด้วยอันตราย.";
		mes "คุณไม่ได้ใช้ชีวิตเว้นแต่คุณจะตกอยู่ในอันตรายเป็นครั้งคราว";
		close;
	}

L_BountyOne:
	mes "[Bounty Board]";
	mes "คุณจะได้รับ ^FF00001 Eden Merit Badge ถ้ารวบรวมทั้งหมดเข้าด้วยกัน.^000000";
	mes "มิฉะนั้น, zeny และ EXP/JEXP ได้รับรางวัลเพียงอย่างเดียว.";
	mes "^FF0000บัญชีวีไอพีจะเลือกวิธีรับรางวัลประสบการณ์^000000.";
	next;
	for (.@i = 4; .@i < getargcount(); .@i += 4)
		.@menu$ = .@menu$ + getmonsterinfo(getarg(.@i),MOB_NAME) + ":";
	.@menu$ = .@menu$ + "Cancel";
	.@bounty = (select(.@menu$) * 4) + 1;
	if (.@bounty > getargcount()){
		mes "^000000[Bounty Board]^000000";
		mes "กลับมาเมื่อคุณได้รับรางวัลที่เสร็จสมบูรณ์.";
		close;
	}
	if (checkquest(getarg(.@bounty),HUNTING) == 2){
		if (vip_status(1)){
			mes "^000000[Bounty Board]^000000";
			mes "คุณจะกระจายประสบการณ์ใหม่ ๆ ของคุณอย่างไร?";
			next;
			.@vip_reward = select("^FF0000ทั้ง Base และ Job EXP^000000", "^0000FFI want Base EXP only.^000000", "^FFA400I want Job EXP only.^000000");
		}
		erasequest getarg(.@bounty);
		if (Zeny + 37500 <= MAX_ZENY)
			Zeny += 37500;
		.@BaseExp = getarg(.@bounty+1);
		.@JobExp = getarg(.@bounty+2);
		switch(.@vip_reward){
			case 2:
				getexp .@BaseExp,0;
				getexp .@JobExp,0;
				break;
			case 3:
				getexp 0,.@BaseExp;
				getexp 0,.@JobExp;
				break;
			default:
				getexp .@BaseExp,.@JobExp;
				break;
		}
	} else {
		mes "^000000[Bounty Board]^000000";
		mes "ค่าหัวในการตามล่า "+getmonsterinfo(getarg(.@bounty-1),MOB_NAME)+" ยังไม่เสร็จสมบูรณ์.";
	}
	close;

L_BountyAll:
	mes "[Bounty Board]";
	mes "โอ้เจ้าคิดว่าตัวเองเป็นนักล่าที่ดีเหรอ?";
	mes "ให้ฉันดูหน่อยว่าเจ้าได้ตามล่าสัตว์ประหลาดทั้งหมดที่เจ้าบอกว่าต้องการหรือไม่.";
	next;
	for (.@i = 5; .@i < getargcount(); .@i += 4){
		if (checkquest(getarg(.@i),HUNTING) < 2){
			mes "[Bounty Board]";
			mes "ภารกิจในการตามล่าทั้งหมดยังไม่เสร็จ";
			mes "การโกหกจะไม่ได้รับการยอมรับ.";
			close;
		}
	}
	if (vip_status(1)){
		mes "^000000[Bounty Board]^000000";
		mes "คุณจะกระจายประสบการณ์ใหม่ ๆ ของคุณอย่างไร?";
		next;
		.@vip_reward = select("^FF0000ทั้ง Base และ Job EXP^000000", "^0000FFฉันต้องการ Base EXP เท่านั้น.^000000", "^FFA400ฉันต้องการ Job EXP เท่านั้น.^000000");
	}
	for (.@i = 5; .@i < getargcount(); .@i += 4)
		erasequest getarg(.@i);
	for (.@i = 6; .@i < getargcount(); .@i += 4){
		if (Zeny + 37500 <= MAX_ZENY)
			Zeny += 37500;
		.@BaseExp = getarg(.@i);
		.@JobExp = getarg(.@i+1);
		switch(.@vip_reward){
			case 2:
				getexp .@BaseExp,0;
				getexp .@JobExp,0;
				break;
			case 3:
				getexp 0,.@BaseExp;
				getexp 0,.@JobExp;
				break;
			default:
				getexp .@BaseExp,.@JobExp;
				break;
		}
	}
	getitem "Para_Logro_Badge",1;
	mes "[Bounty Board]";
	mes "นี่คือรางวัลของคุณ.";
	close;

L_BountyDel:
	mes "[Bounty Board]";
	mes "ข้าจะตรวจสอบภารกิจที่เจ้าได้ทำไปแล้ว.";
	mes "เจ้าต้องการเลิกอันไหน?";
	next;
	for (.@i = 5; .@i < getargcount(); .@i += 4){
		if (checkquest(getarg(.@i),HUNTING) == 1){
			.@monster$ = getmonsterinfo(getarg(.@i-1),MOB_NAME);
			mes "[Bounty Board]";
			mes "ภารกิจในการล่า " + .@monster$ + " คุณยังไม่เสร็จสิ้น.";
			mes "คุณต้องการที่จะดำเนินการต่อหรือเลิก?";
			next;
			if (select("ล่าต่อ "+.@monster$, "^FF0000ล้มเลิกภารกิจ^000000") == 2){
				erasequest getarg(.@i);
				mes "[Bounty Board]";
				mes "บันทึกทั้งหมดของคุณสำหรับภารกิจนี้ถูกลบไปแล้ว.";
				close2;
				.@questerased = 1;
			}
		}
	}
	if (!.@questerased)
		close;
	else
		end;

}
